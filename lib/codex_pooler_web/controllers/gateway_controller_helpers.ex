defmodule CodexPoolerWeb.GatewayControllerHelpers do
  @moduledoc false

  import Phoenix.Controller
  import Plug.Conn

  require Logger

  alias CodexPooler.Access
  alias CodexPooler.Gateway.Admission, as: GatewayAdmission
  alias CodexPooler.Gateway.Contracts
  alias CodexPooler.Gateway.ErrorClassification
  alias CodexPooler.Gateway.ErrorSanitizer
  alias CodexPooler.Gateway.Metadata
  alias CodexPooler.Gateway.OperationalSettings
  alias CodexPooler.Gateway.Payloads.RequestOptions
  alias CodexPooler.Gateway.Payloads.TransportEnvelope
  alias CodexPooler.Platform.ExecutionIdentity
  alias CodexPooler.Platform.TransientDatabaseError
  alias CodexPooler.Pools.Routing, as: PoolRouting
  alias CodexPoolerWeb.RequestLogger

  @type conn :: Plug.Conn.t()
  @type gateway_call_result ::
          {:ok, Contracts.gateway_result()} | {:error, Contracts.gateway_error()}
  @type body_read_result :: {:ok, map()} | {:error, Contracts.gateway_error()}
  @type request_opts :: %{optional(atom()) => term()}
  @type websocket_upgrade_opts :: [
          openai_compatibility: keyword(),
          openai_compatibility_origin: {String.t(), String.t()},
          accepted_turn_state: String.t() | nil
        ]

  @spec admit(conn(), String.t(), (-> gateway_call_result())) :: gateway_call_result()
  @spec admit(conn(), String.t(), map(), (-> gateway_call_result())) :: gateway_call_result()
  def admit(conn, route_class, metadata \\ %{}, fun) when is_function(fun, 0) do
    metadata = Map.merge(metadata, request_metadata(conn))

    GatewayAdmission.run_admitted(route_class, metadata, fun)
  end

  @spec authenticate(conn()) :: {:ok, Access.auth_context()} | {:error, Contracts.gateway_error()}
  def authenticate(%Plug.Conn{private: %{runtime_api_auth: auth}}), do: {:ok, auth}

  def authenticate(conn) do
    header = conn |> get_req_header("authorization") |> List.first()
    authenticate_with(fn -> Access.authenticate_authorization_header(header) end)
  end

  @spec authenticate_v1(conn()) ::
          {:ok, Access.auth_context()} | {:error, Contracts.gateway_error()}
  def authenticate_v1(conn) do
    with {:ok, auth} <- authenticate_v1_auth_context(conn) do
      authorize_v1_compatibility(auth)
    end
  end

  defp authenticate_v1_auth_context(%Plug.Conn{private: %{runtime_api_auth: auth}}),
    do: {:ok, auth}

  defp authenticate_v1_auth_context(conn) do
    header = conn |> get_req_header("authorization") |> List.first()
    authenticate_with(fn -> Access.authenticate_v1_authorization_header(header) end)
  end

  # Authentication is the first database read of every runtime request. A
  # database that cannot be reached, is restarting or cancelled the lookup says
  # nothing about the credential, and nothing has been admitted, reserved or
  # sent yet: answer a retryable 503 instead of letting the exception render a
  # 500 (findings#206 row 206-358).
  defp authenticate_with(fun) do
    case fun.() do
      {:ok, auth} -> {:ok, auth}
      {:error, reason} -> {:error, Map.put(reason, :status, 401)}
    end
  rescue
    error in [DBConnection.ConnectionError, Postgrex.Error] ->
      if TransientDatabaseError.transient?(error) do
        Logger.warning("runtime request refused before admission stage=authentication reason_class=#{TransientDatabaseError.reason_class(error)}")
        {:error, Contracts.database_unavailable_error()}
      else
        reraise error, __STACKTRACE__
      end
  end

  defp authorize_v1_compatibility(%{pool: pool} = auth) do
    if pool.status == "active" and PoolRouting.v1_compatibility_enabled?(pool) do
      {:ok, auth}
    else
      {:error,
       %{
         status: 403,
         code: "v1_compatibility_disabled",
         message: "OpenAI /v1 compatibility is disabled for this pool"
       }}
    end
  end

  defp authorize_v1_compatibility(auth), do: {:ok, auth}

  @spec read_json_body(conn()) :: body_read_result()
  def read_json_body(%Plug.Conn{private: %{runtime_json_parse_error: true}}) do
    {:error, %{status: 400, code: "invalid_request", message: "request body must be valid JSON"}}
  end

  def read_json_body(conn) do
    case conn.body_params do
      %Plug.Conn.Unfetched{} ->
        {:error, %{status: 400, code: "invalid_request", message: "request body must be JSON"}}

      params when is_map(params) ->
        {:ok, params}

      _params ->
        {:error, %{status: 400, code: "invalid_request", message: "request body must be a JSON object"}}
    end
  end

  @spec read_multipart_body(conn()) :: body_read_result()
  def read_multipart_body(conn) do
    case conn.body_params do
      %Plug.Conn.Unfetched{} ->
        {:error,
         %{
           status: 400,
           code: "invalid_request",
           message: "request body must be multipart/form-data"
         }}

      params when is_map(params) ->
        {:ok, params}

      _params ->
        {:error,
         %{
           status: 400,
           code: "invalid_request",
           message: "request body must be multipart/form-data"
         }}
    end
  end

  @spec request_opts(conn()) :: request_opts()
  def request_opts(conn) do
    {session_header_source, session_header} = session_header(conn)

    %{
      request_id: request_id(conn),
      client_request_id: client_request_id(conn),
      idempotency_key: get_req_header(conn, "idempotency-key") |> List.first(),
      accepted_turn_state: accepted_turn_state(conn),
      previous_response_id: previous_response_id(conn),
      session_header: session_header,
      session_header_source: session_header_source,
      user_agent: get_req_header(conn, "user-agent") |> List.first(),
      request_content_type: get_req_header(conn, "content-type") |> List.first(),
      forwarded_headers: forwarded_headers(conn),
      client_ip: conn.remote_ip |> :inet.ntoa() |> to_string()
    }
    |> put_test_owner_liveness_options()
  end

  if Mix.env() == :test do
    @owner_liveness_test_option_keys [
      :bridge_owner_lease_ttl_seconds,
      :session_lease_heartbeat_test_observer,
      :owner_instance_id
    ]

    defp put_test_owner_liveness_options(opts) do
      case Process.get({__MODULE__, :owner_liveness_test_options}) do
        test_opts when is_map(test_opts) ->
          Map.merge(opts, Map.take(test_opts, @owner_liveness_test_option_keys))

        _absent ->
          opts
      end
    end
  else
    defp put_test_owner_liveness_options(opts), do: opts
  end

  @spec websocket_upgrade_opts() :: keyword()
  def websocket_upgrade_opts do
    settings = OperationalSettings.current()

    [
      timeout: settings.websocket_idle_timeout_ms,
      max_frame_size: settings.max_decompressed_body_bytes,
      max_fragmented_message_size: settings.max_decompressed_body_bytes,
      compress: false
    ]
  end

  @spec upgrade_responses_websocket(conn(), Access.auth_context(), websocket_upgrade_opts()) ::
          conn()
  def upgrade_responses_websocket(conn, auth, opts \\ []) do
    turn_state = websocket_turn_state(conn)
    continuity_turn_state = websocket_continuity_turn_state(opts, turn_state)

    request_options =
      conn
      |> request_opts()
      |> RequestOptions.for_websocket()
      |> RequestOptions.capture_api_key_runtime_epoch(auth)
      |> maybe_put_websocket_openai_compatibility(opts)
      |> RequestOptions.put_continuity(
        accepted_turn_state: continuity_turn_state,
        pooler_issued_turn_state?: pooler_issued_turn_state?(conn, turn_state, continuity_turn_state)
      )
      |> maybe_mark_websocket_openai_origin(opts)

    case maybe_put_websocket_models_etag(conn, auth, request_options) do
      {:ok, conn} ->
        conn
        |> put_resp_header("x-codex-turn-state", turn_state)
        |> WebSockAdapter.upgrade(
          CodexPoolerWeb.CodexResponsesSocket,
          %{auth: auth, opts: request_options, firewall_client_ip: conn.remote_ip},
          websocket_upgrade_opts()
        )
        |> halt()

      {:error, reason} ->
        send_error(conn, reason)
    end
  rescue
    error in WebSockAdapter.UpgradeError ->
      send_error(conn, %{
        status: 400,
        code: "websocket_upgrade_required",
        message: Exception.message(error)
      })
  end

  defp maybe_put_websocket_models_etag(conn, auth, %RequestOptions{} = request_options) do
    source_endpoint = request_options.openai_compatibility.source_endpoint || conn.request_path

    if source_endpoint in [
         "/backend-api/codex/responses",
         "/backend-api/codex/v1/responses"
       ] do
      case Metadata.codex_turn_catalog_snapshot(auth, source_endpoint, request_options) do
        {:ok, snapshot} -> {:ok, put_resp_header(conn, "x-models-etag", snapshot.etag)}
        {:error, reason} -> {:error, reason}
      end
    else
      {:ok, conn}
    end
  end

  @spec send_or_error(conn(), gateway_call_result()) :: conn()
  def send_or_error(%Plug.Conn{} = conn, {:ok, result}), do: send_gateway_result(conn, result)
  def send_or_error(%Plug.Conn{} = conn, {:error, reason}), do: send_error(conn, reason)

  @spec result_headers(Contracts.gateway_result() | map()) :: Contracts.response_headers()
  def result_headers(%{headers: headers}) when is_list(headers), do: headers
  def result_headers(_result), do: []

  @spec send_gateway_result(conn(), Contracts.gateway_result()) :: conn()
  def send_gateway_result(conn, result) do
    response = do_send_gateway_result(conn, result)
    ExecutionIdentity.complete()
    response
  end

  @spec send_error(conn(), Contracts.gateway_error() | map()) :: conn()
  def send_error(conn, error) do
    response = do_send_error(conn, error)
    ExecutionIdentity.complete()
    response
  end

  defp do_send_gateway_result(conn, %{stream: stream} = result) do
    conn = put_gateway_headers(conn, result_headers(result))
    conn = send_chunked(conn, result.status)

    case stream.(conn) do
      {:ok, streamed_conn} ->
        streamed_conn

      {:error, reason} ->
        # The response is already chunked, so a late stream error cannot be
        # translated into a structured JSON error for the client.
        Logger.warning([
          "late gateway stream failed",
          " path=#{conn.request_path}",
          " request_id=#{request_id(conn) || "unknown"}",
          " reason=#{ErrorSanitizer.safe_reason(reason)}"
        ])

        conn
    end
  end

  # sobelow_skip ["XSS.SendResp"]
  defp do_send_gateway_result(conn, %{raw_body: body} = result) do
    conn
    |> put_gateway_headers(result_headers(result))
    |> send_resp(result.status, body)
  end

  defp do_send_gateway_result(conn, %{body: body} = result) do
    conn
    |> put_gateway_headers(result_headers(result))
    |> put_status(result.status)
    |> json(body)
  end

  defp do_send_error(conn, %{status: status, code: code, message: message} = error) do
    body = %{
      "error" =>
        Map.merge(
          %{
            "message" => message,
            "type" => ErrorClassification.error_type(code, status),
            "code" => to_string(code),
            "param" => Map.get(error, :param)
          },
          error |> Contracts.recovery_error_fields() |> Map.merge(Contracts.usage_limit_error_fields(error))
        )
    }

    conn
    |> put_policy_retry_header(error)
    |> put_gateway_headers(Contracts.recovery_response_headers(error))
    |> put_gateway_headers(Contracts.usage_limit_response_headers(error))
    |> RequestLogger.put_usage_limit(Contracts.usage_limit_record(error))
    |> put_gateway_headers(Contracts.circuit_retry_response_headers(error))
    |> put_status(status)
    |> json(body)
  end

  defp do_send_error(conn, %{code: code, message: message}) do
    send_error(conn, %{status: 401, code: code, message: message})
  end

  # Minimum backoff advice; it does not promise that a slot will be available.
  defp put_policy_retry_header(conn, %{
         pooler_policy: true,
         status: 429,
         code: "api_key_concurrency_limit_exceeded"
       }),
       do: put_resp_header(conn, "retry-after", "1")

  # A key policy window's own boundary (findings#206 row 206-427); advice, not
  # a promise: settling in-flight work can free the window earlier. A window
  # that frees in a minute at the soonest (the daily window's hint, the weekly
  # window without one) also tells the OpenAI SDKs, which retry every 429 twice
  # within seconds when the hint exceeds their own ceiling, not to retry.
  defp put_policy_retry_header(conn, %{pooler_policy: true, status: 429, code: "api_key_policy_limit_exceeded"} = error) do
    case Map.get(error, :retry_after_seconds) do
      seconds when is_integer(seconds) and seconds > 0 and seconds <= 60 ->
        put_resp_header(conn, "retry-after", Integer.to_string(seconds))

      seconds when is_integer(seconds) and seconds > 0 ->
        conn
        |> put_resp_header("retry-after", Integer.to_string(seconds))
        |> put_resp_header("x-should-retry", "false")

      _none ->
        put_resp_header(conn, "x-should-retry", "false")
    end
  end

  defp put_policy_retry_header(conn, _error), do: conn

  defp forwarded_headers(conn) do
    provider_session_header_names = TransportEnvelope.provider_session_header_names()

    Enum.filter(conn.req_headers, fn {name, _value} ->
      name == "user-agent" or String.starts_with?(name, "x-openai-") or
        String.starts_with?(name, "x-codex-") or name in provider_session_header_names
    end)
  end

  defp accepted_turn_state(conn) do
    conn
    |> get_req_header("x-codex-turn-state")
    |> List.first()
    |> blank_to_nil()
  end

  # The released Codex client never sends `x-codex-turn-state` on an upgrade:
  # the token is server-issued, so a websocket upgrade without one gets a fresh
  # value echoed on the upgrade response (findings#255).
  defp websocket_turn_state(conn), do: accepted_turn_state(conn) || Ecto.UUID.generate()

  defp pooler_issued_turn_state?(conn, issued_turn_state, continuity_turn_state) do
    continuity_turn_state == issued_turn_state and is_nil(accepted_turn_state(conn))
  end

  defp websocket_continuity_turn_state(opts, turn_state) do
    case Keyword.fetch(opts, :accepted_turn_state) do
      {:ok, value} -> value
      :error -> turn_state
    end
  end

  defp maybe_put_websocket_openai_compatibility(%RequestOptions{} = request_options, opts) do
    case Keyword.get(opts, :openai_compatibility, []) do
      [] ->
        request_options

      compatibility when is_list(compatibility) ->
        RequestOptions.put_openai_compatibility(request_options, compatibility)
    end
  end

  defp maybe_mark_websocket_openai_origin(%RequestOptions{} = request_options, opts) do
    case Keyword.get(opts, :openai_compatibility_origin) do
      {public_endpoint, backend_endpoint} ->
        RequestOptions.mark_openai_compatibility_origin(
          request_options,
          public_endpoint,
          backend_endpoint
        )

      _origin ->
        request_options
    end
  end

  defp previous_response_id(conn) do
    conn
    |> get_req_header("x-codex-previous-response-id")
    |> List.first()
    |> blank_to_nil()
  end

  defp session_header(conn) do
    [
      "x-codex-window-id",
      "x-codex-session-id",
      "session-id",
      "x-session-id",
      "x-session-affinity",
      "session_id",
      "x-codex-conversation-id"
    ]
    |> Enum.find_value({nil, nil}, fn header ->
      case conn |> get_req_header(header) |> List.first() |> blank_to_nil() do
        nil -> false
        value -> {header, value}
      end
    end)
  end

  defp request_id(conn) do
    List.first(get_req_header(conn, "x-request-id")) ||
      List.first(get_resp_header(conn, "x-request-id"))
  end

  defp client_request_id(conn), do: List.first(get_req_header(conn, "x-request-id"))

  defp request_metadata(conn) do
    %{
      request_id: request_id(conn),
      method: conn.method,
      path: "/" <> Enum.join(conn.path_info, "/")
    }
  end

  defp blank_to_nil(value) when is_binary(value) do
    value = String.trim(value)
    if value == "", do: nil, else: value
  end

  defp blank_to_nil(_value), do: nil

  defp put_gateway_headers(conn, headers) do
    Enum.reduce(headers, conn, fn {key, value}, conn -> put_resp_header(conn, key, value) end)
  end
end
