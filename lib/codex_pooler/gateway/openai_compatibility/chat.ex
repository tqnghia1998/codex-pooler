defmodule CodexPooler.Gateway.OpenAICompatibility.Chat do
  @moduledoc false

  alias CodexPooler.Gateway.OpenAICompatibility.{Error, Matrix, Responses, Validation}
  alias CodexPooler.Gateway.OpenAICompatibility.Responses.Input.Normalization
  alias CodexPooler.Gateway.Payloads.RequestOptions
  alias CodexPooler.ServiceTier

  @locally_unsupported_fields ~w(audio frequency_penalty logit_bias logprobs modalities n prediction presence_penalty seed stop top_logprobs web_search_options)
  @responses_fallback_fields ~w(input include reasoning text)
  @service_tiers ~w(auto default flex priority scale)
  @verbosity_values ~w(low medium high)

  @spec validate(term()) :: {:ok, map()} | {:error, Error.reason()}
  def validate(payload) do
    with {:ok, %{chat_payload: chat_payload, response_payload: response_payload}} <-
           prepare_response_payload(payload),
         {:ok, _response_payload} <- Responses.validate(response_payload, surface: :chat) do
      {:ok, chat_payload}
    end
  end

  @spec coerce(term(), map() | keyword()) ::
          {:ok,
           %{
             endpoint: String.t(),
             payload: map(),
             request_options: RequestOptions.t(),
             chat_payload: map()
           }}
          | {:error, Error.reason()}
  def coerce(payload, opts \\ %{}) do
    with {:ok, %{chat_payload: chat_payload, response_payload: response_payload}} <-
           prepare_response_payload(payload),
         {:ok, response} <- Responses.coerce(response_payload, put_surface(opts, :chat)) do
      {:ok, Map.put(response, :chat_payload, chat_payload)}
    end
  end

  @doc """
  Maps an upstream Responses parameter path from a relayed validation
  rejection back to the Chat Completions field the client sent. Only the
  renames this adapter performs on the messages path are reversed, and only
  when the client actually sent the source field; every other path, and every
  fallback-input request, is returned unchanged.
  """
  @spec public_validation_param(String.t(), map()) :: String.t()
  def public_validation_param(param, %{"messages" => [_message | _rest]} = chat_payload)
      when is_binary(param),
      do: chat_validation_param(param, chat_payload)

  def public_validation_param(param, _chat_payload), do: param

  defp chat_validation_param("reasoning.effort", %{"reasoning_effort" => _effort}),
    do: "reasoning_effort"

  defp chat_validation_param("max_output_tokens", %{"max_completion_tokens" => _value}),
    do: "max_completion_tokens"

  defp chat_validation_param("max_output_tokens", %{"max_tokens" => _value}),
    do: "max_tokens"

  defp chat_validation_param("text.verbosity", %{"verbosity" => _verbosity}), do: "verbosity"

  defp chat_validation_param("text.format" <> rest, %{"response_format" => %{} = format}),
    do: response_format_validation_param(rest, format) || "text.format" <> rest

  defp chat_validation_param("tool_choice.name", %{"tool_choice" => %{"type" => type} = choice})
       when type in ["function", "custom"] and is_map_key(choice, type),
       do: "tool_choice." <> type <> ".name"

  defp chat_validation_param("tools[" <> _rest = param, %{"tools" => tools})
       when is_list(tools),
       do: tool_validation_param(param, tools)

  # The adapter rebuilds `messages` into Responses input items, not one item
  # per message (every tool call and some content parts become items of their
  # own, an assistant message with empty content and tool calls leaves none,
  # Lite prepends Pooler items), so no `input` path, index or item field names
  # anything this client sent. The relay names the field that carried the
  # refused item instead of a Responses path (findings#254 row 254-54).
  defp chat_validation_param("input", _chat_payload), do: "messages"
  defp chat_validation_param("input[" <> _rest, _chat_payload), do: "messages"
  defp chat_validation_param("input." <> _rest, _chat_payload), do: "messages"

  defp chat_validation_param(param, _chat_payload), do: param

  defp response_format_validation_param("", %{"type" => type})
       when type in ["json_object", "json_schema", "text"],
       do: "response_format"

  defp response_format_validation_param(".type", %{"type" => type})
       when type in ["json_object", "json_schema", "text"],
       do: "response_format.type"

  defp response_format_validation_param("." <> _field = rest, %{
         "type" => "json_schema",
         "json_schema" => %{}
       }),
       do: "response_format.json_schema" <> rest

  defp response_format_validation_param(_rest, _format), do: nil

  defp tool_validation_param(param, tools) do
    with [_match, index, field, rest] <-
           Regex.run(~r/\Atools\[(0|[1-9][0-9]{0,3})\]\.([A-Za-z_]+)(.*)\z/, param),
         %{"type" => type} = tool when type in ["function", "custom"] <-
           Enum.at(tools, String.to_integer(index)),
         true <- is_map(Map.get(tool, type)) and field in nested_tool_fields(type) do
      "tools[" <> index <> "]." <> type <> "." <> field <> rest
    else
      _other -> param
    end
  end

  defp nested_tool_fields("function"), do: ["name", "description", "parameters", "strict"]
  defp nested_tool_fields("custom"), do: ["name", "description", "format"]

  defp put_surface(opts, surface) when is_list(opts), do: Keyword.put(opts, :surface, surface)
  defp put_surface(opts, surface) when is_map(opts), do: Map.put(opts, :surface, surface)

  defp prepare_response_payload(payload) do
    with {:ok, payload} <- Validation.normalize_payload(payload),
         :ok <- reject_legacy_functions(payload),
         :ok <- Validation.reject_high_impact_fields(payload),
         :ok <- Validation.reject_unsupported_fields(payload, :chat),
         :ok <- Validation.require_model(payload),
         {:ok, payload} <- discard_user_identifier(payload),
         :ok <- reject_responses_fallback_fields_with_messages(payload),
         :ok <- reject_locally_unsupported_fields(payload),
         :ok <- validate_reasoning_effort(payload),
         :ok <- validate_service_tier(payload),
         :ok <- validate_stream_options(payload),
         :ok <- validate_token_limits(payload),
         :ok <- validate_verbosity(payload),
         :ok <- validate_translatable_prompt_cache_breakpoints(payload),
         :ok <- validate_image_details(payload),
         :ok <- reject_namespaced_custom_tool_definitions(payload),
         :ok <- validate_custom_tool_choice_shape(payload),
         {:ok, response_payload} <- response_payload(payload) do
      {:ok, %{chat_payload: payload, response_payload: response_payload}}
    end
  end

  defp discard_user_identifier(%{"user" => user} = payload)
       when is_binary(user) or is_nil(user),
       do: {:ok, Map.delete(payload, "user")}

  defp discard_user_identifier(%{"user" => _user}),
    do: {:error, Error.invalid_request("user must be a string or null", "user")}

  defp discard_user_identifier(payload), do: {:ok, payload}

  defp reject_legacy_functions(payload) do
    cond do
      Map.has_key?(payload, "functions") ->
        {:error, Error.invalid_request("legacy functions are not translatable", "functions")}

      Map.has_key?(payload, "function_call") ->
        {:error, Error.invalid_request("legacy function_call is not translatable", "function_call")}

      true ->
        :ok
    end
  end

  defp reject_namespaced_custom_tool_definitions(%{"tools" => tools}) when is_list(tools) do
    if Enum.any?(tools, &namespaced_custom_tool_definition?/1),
      do: {:error, Error.invalid_request("tool shape is not translatable", "tools")},
      else: :ok
  end

  defp reject_namespaced_custom_tool_definitions(_payload), do: :ok

  defp namespaced_custom_tool_definition?(%{"type" => "namespace", "tools" => tools})
       when is_list(tools),
       do: Enum.any?(tools, &custom_tool_definition?/1)

  defp namespaced_custom_tool_definition?(_tool), do: false

  defp custom_tool_definition?(%{"type" => "custom"}), do: true

  defp custom_tool_definition?(%{"tools" => tools}) when is_list(tools),
    do: Enum.any?(tools, &custom_tool_definition?/1)

  defp custom_tool_definition?(_tool), do: false

  defp messages(%{"messages" => messages}) when is_list(messages) and messages != [] do
    if Enum.all?(messages, &valid_message?/1) do
      {:ok, messages}
    else
      {:error, Error.invalid_request("messages must contain role/content objects", "messages")}
    end
  end

  defp messages(%{"messages" => _messages}),
    do: {:error, Error.invalid_request("messages must be a non-empty array", "messages")}

  defp messages(_payload), do: {:error, Error.invalid_request("messages is required", "messages")}

  defp reject_locally_unsupported_fields(payload) do
    payload
    |> Map.keys()
    |> Enum.find(&(&1 in @locally_unsupported_fields))
    |> case do
      nil -> :ok
      field -> {:error, Error.unsupported_parameter(field)}
    end
  end

  defp reject_responses_fallback_fields_with_messages(%{"messages" => messages} = payload)
       when is_list(messages) and messages != [] do
    case Enum.find(@responses_fallback_fields, &Map.has_key?(payload, &1)) do
      nil ->
        :ok

      field ->
        {:error,
         Error.invalid_request(
           "Responses fields cannot be combined with non-empty messages",
           field
         )}
    end
  end

  defp reject_responses_fallback_fields_with_messages(_payload), do: :ok

  defp validate_reasoning_effort(%{"reasoning_effort" => effort}),
    do: Validation.validate_reasoning_effort_token(effort, "reasoning_effort")

  defp validate_reasoning_effort(_payload), do: :ok

  defp validate_service_tier(%{"service_tier" => tier}) when is_binary(tier) do
    tier = ServiceTier.canonicalize(tier)

    if tier in @service_tiers,
      do: :ok,
      else: {:error, Error.invalid_request("service_tier is not supported", "service_tier")}
  end

  defp validate_service_tier(%{"service_tier" => _tier}),
    do: {:error, Error.invalid_request("service_tier is not supported", "service_tier")}

  defp validate_service_tier(_payload), do: :ok

  defp validate_stream_options(%{"stream_options" => options}) when is_map(options) do
    with :ok <- validate_stream_option_keys(options, ["include_usage"]) do
      case Map.get(options, "include_usage") do
        nil ->
          :ok

        value when is_boolean(value) ->
          :ok

        _value ->
          {:error,
           Error.invalid_request(
             "stream_options.include_usage must be a boolean",
             "stream_options.include_usage"
           )}
      end
    end
  end

  defp validate_stream_options(%{"stream_options" => _options}),
    do: {:error, Error.invalid_request("stream_options must be an object", "stream_options")}

  defp validate_stream_options(_payload), do: :ok

  defp validate_stream_option_keys(options, allowed_keys) do
    case options |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] ->
        :ok

      [key | _rest] ->
        {:error, Error.invalid_request("stream_options field is not supported", "stream_options." <> key)}
    end
  end

  defp validate_token_limits(payload) do
    ["max_tokens", "max_completion_tokens"]
    |> Enum.find_value(:ok, fn field ->
      case Map.fetch(payload, field) do
        {:ok, value} when is_integer(value) and value > 0 ->
          false

        {:ok, _value} ->
          {:error, Error.invalid_request(field <> " must be a positive integer", field)}

        :error ->
          false
      end
    end)
  end

  defp validate_verbosity(%{"verbosity" => verbosity}) when is_binary(verbosity) do
    verbosity = normalize_enum(verbosity)

    if verbosity in @verbosity_values,
      do: :ok,
      else: {:error, Error.invalid_request("verbosity is not supported", "verbosity")}
  end

  defp validate_verbosity(%{"verbosity" => _verbosity}),
    do: {:error, Error.invalid_request("verbosity is not supported", "verbosity")}

  defp validate_verbosity(_payload), do: :ok

  defp valid_message?(%{"role" => "assistant", "tool_calls" => tool_calls} = message)
       when is_list(tool_calls) do
    (valid_assistant_tool_message_content?(Map.get(message, "content")) or
       (message["content"] == [] and tool_calls != [])) and
      valid_assistant_tool_calls?(tool_calls)
  end

  defp valid_message?(%{"role" => "tool", "content" => content}),
    do: valid_tool_content?(content)

  defp valid_message?(%{"role" => role, "content" => content})
       when role in ["system", "user", "assistant", "developer", "tool"] do
    valid_content?(content)
  end

  defp valid_message?(_message), do: false

  defp response_payload(%{"messages" => messages} = payload)
       when is_list(messages) and messages != [] do
    with {:ok, messages} <- messages(payload) do
      response_payload_from_messages(payload, messages)
    end
  end

  defp response_payload(%{"messages" => []} = payload) do
    fallback_response_payload(
      payload,
      Error.invalid_request("messages must be a non-empty array", "messages")
    )
  end

  defp response_payload(%{"messages" => _messages} = payload) do
    with {:ok, _messages} <- messages(payload) do
      fallback_response_payload(
        payload,
        Error.invalid_request("messages is required", "messages")
      )
    end
  end

  defp response_payload(payload) do
    fallback_response_payload(payload, Error.invalid_request("messages is required", "messages"))
  end

  defp fallback_response_payload(%{"input" => _input} = payload, _messages_error) do
    payload
    |> Map.take(Matrix.forwarded_fields(:responses))
    |> Map.delete("stream_options")
    |> Map.put_new("instructions", "")
    |> then(&{:ok, &1})
  end

  defp fallback_response_payload(_payload, messages_error), do: {:error, messages_error}

  defp response_payload_from_messages(payload, messages) do
    base = %{
      "model" => payload["model"],
      "input" => Enum.flat_map(messages, &message_to_input_items/1)
    }

    with {:ok, base} <- maybe_put_tools(base, payload) do
      base
      |> maybe_put_tool_choice(payload)
      |> maybe_put(payload, "parallel_tool_calls")
      |> maybe_put(payload, "metadata")
      |> maybe_put(payload, "moderation")
      |> maybe_put(payload, "prompt_cache_key")
      |> maybe_put(payload, "prompt_cache_options")
      |> maybe_put(payload, "prompt_cache_retention")
      |> maybe_put(payload, "safety_identifier")
      |> maybe_put(payload, "service_tier")
      |> maybe_put(payload, "store")
      |> maybe_put(payload, "stream")
      |> maybe_put(payload, "temperature")
      |> maybe_put(payload, "top_p")
      |> maybe_put_max_output_tokens(payload)
      |> maybe_put_reasoning(payload)
      |> put_text_options(payload)
    end
  end

  defp message_to_input_items(%{"role" => "assistant", "tool_calls" => tool_calls} = message)
       when is_list(tool_calls) do
    assistant_tool_message_content_items(message) ++ assistant_tool_call_items(tool_calls)
  end

  defp message_to_input_items(%{"role" => role, "content" => content} = message) do
    content
    |> content_parts()
    |> expand_message_parts(role, message)
  end

  defp assistant_tool_message_content_items(message) do
    case Map.fetch(message, "content") do
      {:ok, nil} -> []
      {:ok, ""} -> []
      {:ok, content} -> content |> content_parts() |> expand_message_parts("assistant", message)
      :error -> []
    end
  end

  defp content_parts(content) when is_binary(content), do: [content]
  defp content_parts(content) when is_list(content), do: content
  defp content_parts(%{} = content), do: [content]
  defp content_parts(content), do: [content]

  defp expand_message_parts(parts, role, message) do
    parts
    |> Enum.reduce({[], []}, fn part, {items, pending_parts} ->
      case special_content_item(part) do
        nil ->
          {items, [normalize_content_part(part, role) | pending_parts]}

        item ->
          items = flush_message_item(items, pending_parts, role, message)
          {[item | items], []}
      end
    end)
    |> then(fn {items, pending_parts} ->
      items |> flush_message_item(pending_parts, role, message) |> Enum.reverse()
    end)
  end

  defp flush_message_item(items, [], _role, _message), do: items

  defp flush_message_item(items, pending_parts, role, message) do
    item =
      %{"type" => "message", "role" => role, "content" => Enum.reverse(pending_parts)}
      |> maybe_put(message, "name")
      |> maybe_put(message, "tool_call_id")

    [item | items]
  end

  defp normalize_content_part(text, "assistant") when is_binary(text),
    do: %{"type" => "output_text", "text" => text}

  defp normalize_content_part(text, _role) when is_binary(text),
    do: %{"type" => "input_text", "text" => text}

  defp normalize_content_part(%{"type" => "text", "text" => text} = part, "assistant"),
    do:
      %{"type" => "output_text", "text" => text}
      |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(%{"type" => "text", "text" => text} = part, _role),
    do:
      %{"type" => "input_text", "text" => text}
      |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(%{"type" => "input_text", "text" => text} = part, _role),
    do:
      %{"type" => "input_text", "text" => text}
      |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(%{"type" => "image_url", "image_url" => image_url} = part, _role)
       when is_binary(image_url),
       do:
         %{"type" => "input_image", "image_url" => image_url}
         |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(
         %{"type" => "image_url", "image_url" => %{"url" => image_url} = image} = part,
         _role
       )
       when is_binary(image_url),
       do:
         %{"type" => "input_image", "image_url" => image_url}
         |> maybe_put_image_detail(image)
         |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(
         %{"type" => "file", "file" => %{"file_id" => file_id}} = part,
         _role
       )
       when is_binary(file_id) and file_id != "",
       do:
         %{"type" => "input_file", "file_id" => file_id}
         |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(
         %{"type" => "file", "file" => %{"filename" => filename, "file_data" => file_data}} = part,
         _role
       )
       when is_binary(filename) and filename != "" and is_binary(file_data),
       do:
         %{"type" => "input_file", "filename" => filename, "file_data" => file_data}
         |> maybe_put_prompt_cache_breakpoint(part)

  defp normalize_content_part(%{"type" => "input_audio"} = part, _role), do: part
  defp normalize_content_part(%{} = part, _role), do: part
  defp normalize_content_part(content, _role), do: content

  defp special_content_item(%{"type" => "tool-call"} = part), do: cline_tool_call_item(part)
  defp special_content_item(%{"type" => "tool-result"} = part), do: cline_tool_result_item(part)
  defp special_content_item(_part), do: nil

  defp cline_tool_call_item(%{"toolCallId" => call_id, "toolName" => name, "input" => input})
       when is_binary(call_id) and call_id != "" and is_binary(name) and name != "" do
    %{
      "type" => "function_call",
      "call_id" => call_id,
      "name" => name,
      "arguments" => CodexPooler.JSON.encode!(input)
    }
  end

  defp cline_tool_call_item(part), do: part

  defp cline_tool_result_item(%{"toolCallId" => call_id, "output" => output})
       when is_binary(call_id) and call_id != "" do
    %{
      "type" => "function_call_output",
      "call_id" => call_id,
      "output" => normalize_cline_tool_result_output(output)
    }
  end

  defp cline_tool_result_item(part), do: part

  defp assistant_tool_call_items(tool_calls),
    do: Enum.map(tool_calls, &assistant_tool_call_item/1)

  defp assistant_tool_call_item(%{"function" => %{"name" => name, "arguments" => arguments}} = item) do
    %{
      "type" => "function_call",
      "call_id" => assistant_tool_call_id(item),
      "name" => name,
      "arguments" => arguments
    }
  end

  defp assistant_tool_call_id(item) do
    clean_string(Map.get(item, "call_id")) || clean_string(Map.get(item, "id"))
  end

  defp clean_string(value) when is_binary(value) do
    value = String.trim(value)
    if value == "", do: nil, else: value
  end

  defp clean_string(_value), do: nil

  defp normalize_cline_tool_result_output(output) when is_binary(output), do: output

  defp normalize_cline_tool_result_output(output) when is_list(output),
    do: Enum.map(output, &normalize_cline_tool_result_output_part/1)

  defp normalize_cline_tool_result_output(output), do: output

  defp normalize_cline_tool_result_output_part(part) when is_binary(part),
    do: %{"type" => "input_text", "text" => part}

  defp normalize_cline_tool_result_output_part(%{"type" => type, "text" => text})
       when type in ["text", "input_text"] and is_binary(text),
       do: %{"type" => "input_text", "text" => text}

  defp normalize_cline_tool_result_output_part(%{"type" => "image_url", "image_url" => image_url})
       when is_binary(image_url),
       do: %{"type" => "input_image", "image_url" => image_url}

  # A tool-result image keeps its detail like a message image (findings#206
  # row 206-494); `validate_image_details/1` admits only an enum value.
  defp normalize_cline_tool_result_output_part(%{
         "type" => "image_url",
         "image_url" => %{"url" => image_url} = image
       })
       when is_binary(image_url),
       do: %{"type" => "input_image", "image_url" => image_url} |> maybe_put_image_detail(image)

  defp normalize_cline_tool_result_output_part(%{"type" => "input_image", "image_url" => image_url} = part)
       when is_binary(image_url),
       do: %{"type" => "input_image", "image_url" => image_url} |> maybe_put_image_detail(part)

  defp normalize_cline_tool_result_output_part(%{
         "type" => "image",
         "data" => data,
         "mediaType" => media_type
       })
       when is_binary(data) and is_binary(media_type),
       do: %{"type" => "input_image", "image_url" => "data:#{media_type};base64,#{data}"}

  defp normalize_cline_tool_result_output_part(%{"type" => "json", "value" => value}),
    do: %{"type" => "input_text", "text" => CodexPooler.JSON.encode!(value)}

  defp normalize_cline_tool_result_output_part(part), do: part

  defp valid_tool_content?(content) when is_binary(content), do: true

  defp valid_tool_content?(content) when is_list(content) do
    content != [] and Enum.all?(content, &valid_tool_content_part?/1)
  end

  defp valid_tool_content?(_content), do: false

  defp valid_tool_content_part?(%{"type" => type, "text" => text})
       when type in ["text", "input_text"] and is_binary(text),
       do: true

  # Hermes in its default `chat_completions` mode sends a screenshot tool
  # result as `image_url` parts of the tool message. The rebuild carries them
  # into the `function_call_output`, where the Codex backend accepts an image
  # (findings#206 row 206-476, probed on `gpt-6-luna`); a file part has no
  # tool-output form there and stays refused.
  defp valid_tool_content_part?(%{"type" => "image_url", "image_url" => image_url})
       when is_binary(image_url),
       do: true

  defp valid_tool_content_part?(%{"type" => "image_url", "image_url" => %{"url" => image_url}})
       when is_binary(image_url),
       do: true

  defp valid_tool_content_part?(_part), do: false

  defp validate_translatable_prompt_cache_breakpoints(%{"messages" => messages})
       when is_list(messages) do
    messages
    |> Enum.find_value(:ok, &prompt_cache_breakpoint_translation_error/1)
  end

  defp validate_translatable_prompt_cache_breakpoints(_payload), do: :ok

  defp prompt_cache_breakpoint_translation_error(%{"role" => "assistant", "content" => content}) do
    if content
       |> content_parts()
       |> Enum.any?(&marked_text_content_part?/1) do
      {:error, Error.invalid_request("assistant prompt_cache_breakpoint is not translatable", "input")}
    end
  end

  defp prompt_cache_breakpoint_translation_error(%{"role" => "user", "content" => content}) do
    if content
       |> content_parts()
       |> Enum.any?(&marked_input_audio_content_part?/1) do
      {:error, Error.invalid_request("input_audio prompt_cache_breakpoint is not translatable", "input")}
    end
  end

  defp prompt_cache_breakpoint_translation_error(_message), do: false

  defp marked_text_content_part?(%{
         "type" => type,
         "text" => _text,
         "prompt_cache_breakpoint" => _breakpoint
       })
       when type in ["text", "input_text"],
       do: true

  defp marked_text_content_part?(_part), do: false

  defp marked_input_audio_content_part?(%{
         "type" => "input_audio",
         "prompt_cache_breakpoint" => _breakpoint
       }),
       do: true

  defp marked_input_audio_content_part?(_part), do: false

  defp maybe_put_prompt_cache_breakpoint(acc, %{"prompt_cache_breakpoint" => breakpoint}),
    do: Map.put(acc, "prompt_cache_breakpoint", breakpoint)

  defp maybe_put_prompt_cache_breakpoint(acc, _part), do: acc

  # `image_url.detail` becomes the `input_image` detail, as the Responses
  # adapter forwards it; a null detail stays absent and Lite strips the rest
  # in the payload normalizer. Only an enum value reaches here
  # (`validate_image_details/1`).
  defp maybe_put_image_detail(acc, %{"detail" => detail}) when is_binary(detail),
    do: Map.put(acc, "detail", detail)

  defp maybe_put_image_detail(acc, _image), do: acc

  # The public Chat Completions API accepts `image_url.detail` (gpt-6-luna,
  # probed 2026-09-24), and the Codex backend refuses a value outside low,
  # high, auto and original on the `input_image` the rebuild produces
  # (findings#206 row 206-476). Such a value is refused here, before
  # reservation or dispatch and on every serving mode, under the Chat field
  # the client sent rather than a rebuilt `input` path. A Responses-shaped
  # `input_image` part in a Chat message is checked under its own `detail`.
  defp validate_image_details(%{"messages" => messages}) when is_list(messages) do
    messages
    |> Enum.with_index()
    |> Enum.find_value(:ok, fn
      {%{"content" => content}, index} when is_list(content) ->
        content
        |> Enum.with_index()
        |> Enum.find_value(fn {part, part_index} -> invalid_image_detail(part, "messages[#{index}].content[#{part_index}]") end)

      {%{"content" => %{} = part}, index} ->
        invalid_image_detail(part, "messages[#{index}].content")

      _message ->
        nil
    end)
  end

  defp validate_image_details(_payload), do: :ok

  defp invalid_image_detail(%{"type" => "image_url", "image_url" => %{"detail" => detail}}, path),
    do: unless(Normalization.valid_image_detail?(detail), do: {:error, Normalization.invalid_image_detail(path <> ".image_url.detail")})

  defp invalid_image_detail(%{"type" => "input_image", "detail" => detail}, path),
    do: unless(Normalization.valid_image_detail?(detail), do: {:error, Normalization.invalid_image_detail(path <> ".detail")})

  # A Cline `tool-result` carries its images in `output`; their detail is
  # checked under `.output[k]` of the Chat field (findings#206 row 206-494).
  defp invalid_image_detail(%{"type" => "tool-result", "output" => output}, path) when is_list(output) do
    output
    |> Enum.with_index()
    |> Enum.find_value(fn {part, output_index} -> invalid_image_detail(part, "#{path}.output[#{output_index}]") end)
  end

  defp invalid_image_detail(_part, _path), do: nil

  defp valid_content?(content) when is_binary(content), do: true

  defp valid_content?(content) when is_list(content),
    do: content != [] and Enum.all?(content, &valid_content_part?/1)

  defp valid_content?(%{} = content), do: valid_content_part?(content)
  defp valid_content?(_content), do: false

  defp valid_assistant_tool_message_content?(nil), do: true
  defp valid_assistant_tool_message_content?(content), do: valid_content?(content)

  defp valid_assistant_tool_calls?(tool_calls),
    do: Enum.all?(tool_calls, &valid_assistant_tool_call?/1)

  defp valid_assistant_tool_call?(%{"function" => %{"name" => name, "arguments" => arguments}} = item)
       when is_binary(name) and name != "" and is_binary(arguments) do
    assistant_tool_call_id(item) != nil
  end

  defp valid_assistant_tool_call?(_tool_call), do: false

  defp valid_content_part?(%{"type" => type, "text" => text})
       when type in ["text", "input_text"] and is_binary(text),
       do: true

  defp valid_content_part?(%{"type" => "image_url", "image_url" => image_url})
       when is_binary(image_url),
       do: true

  defp valid_content_part?(%{"type" => "image_url", "image_url" => %{"url" => image_url}})
       when is_binary(image_url),
       do: true

  defp valid_content_part?(%{"type" => "input_image", "image_url" => image_url})
       when is_binary(image_url),
       do: true

  defp valid_content_part?(%{
         "type" => "input_audio",
         "input_audio" => %{"data" => data, "format" => format}
       })
       when is_binary(data) and is_binary(format),
       do: true

  defp valid_content_part?(%{"type" => "file", "file" => %{"file_id" => file_id}})
       when is_binary(file_id) and file_id != "",
       do: true

  defp valid_content_part?(%{
         "type" => "file",
         "file" => %{"filename" => filename, "file_data" => file_data}
       })
       when is_binary(filename) and filename != "" and is_binary(file_data),
       do: true

  defp valid_content_part?(%{
         "type" => "tool-call",
         "toolCallId" => call_id,
         "toolName" => name,
         "input" => _input
       })
       when is_binary(call_id) and call_id != "" and is_binary(name) and name != "",
       do: true

  defp valid_content_part?(%{
         "type" => "tool-result",
         "toolCallId" => call_id,
         "output" => output
       })
       when is_binary(call_id) and call_id != "",
       do: valid_cline_tool_result_output?(output)

  defp valid_content_part?(_part), do: false

  defp valid_cline_tool_result_output?(output) when is_binary(output), do: true

  defp valid_cline_tool_result_output?(output) when is_list(output),
    do: output != [] and Enum.all?(output, &valid_cline_tool_result_output_part?/1)

  defp valid_cline_tool_result_output?(output) when is_map(output), do: true
  defp valid_cline_tool_result_output?(_output), do: false

  defp valid_cline_tool_result_output_part?(part) when is_binary(part), do: true

  defp valid_cline_tool_result_output_part?(%{"type" => type, "text" => text})
       when type in ["text", "input_text"] and is_binary(text),
       do: true

  defp valid_cline_tool_result_output_part?(%{"type" => "image_url", "image_url" => image_url})
       when is_binary(image_url),
       do: true

  defp valid_cline_tool_result_output_part?(%{
         "type" => "image_url",
         "image_url" => %{"url" => image_url}
       })
       when is_binary(image_url),
       do: true

  defp valid_cline_tool_result_output_part?(%{
         "type" => "input_image",
         "image_url" => image_url
       })
       when is_binary(image_url),
       do: true

  defp valid_cline_tool_result_output_part?(%{
         "type" => "image",
         "data" => data,
         "mediaType" => media_type
       })
       when is_binary(data) and is_binary(media_type),
       do: true

  defp valid_cline_tool_result_output_part?(%{"type" => "json", "value" => _value}), do: true
  defp valid_cline_tool_result_output_part?(_part), do: false

  defp maybe_put_tools(acc, %{"tools" => tools}) when is_list(tools) do
    with {:ok, tools} <- translate_tools(tools) do
      {:ok, Map.put(acc, "tools", tools)}
    end
  end

  defp maybe_put_tools(_acc, %{"tools" => _tools}),
    do: {:error, Error.invalid_request("tools must be an array", "tools")}

  defp maybe_put_tools(acc, _payload), do: {:ok, acc}

  defp translate_tools(tools) do
    tools
    |> Enum.reduce_while({:ok, []}, fn tool, {:ok, acc} ->
      case translate_tool(tool) do
        {:ok, translated} -> {:cont, {:ok, [translated | acc]}}
        {:error, reason} -> {:halt, {:error, reason}}
      end
    end)
    |> case do
      {:ok, tools} -> {:ok, Enum.reverse(tools)}
      {:error, reason} -> {:error, reason}
    end
  end

  defp translate_tool(%{
         "type" => "function",
         "function" => %{"name" => name, "parameters" => parameters} = function
       })
       when is_binary(name) and is_map(parameters) do
    if String.trim(name) == "" do
      {:error, Error.invalid_request("function tool requires a non-empty name", "tools")}
    else
      tool =
        function
        |> Map.take(["name", "description", "parameters", "strict"])
        |> Map.put("type", "function")

      {:ok, tool}
    end
  end

  defp translate_tool(%{"type" => "function"}),
    do:
      {:error,
       Error.invalid_request(
         "function tool requires nested function name and parameters",
         "tools"
       )}

  defp translate_tool(%{"type" => "custom", "custom" => %{} = custom} = tool) do
    with :ok <- validate_exact_custom_keys(tool, ["type", "custom"]),
         :ok <- validate_exact_custom_keys(custom, ["name", "description", "format"]) do
      {:ok, Map.put(custom, "type", "custom")}
    end
  end

  defp translate_tool(%{"type" => "custom", "name" => name} = tool)
       when is_binary(name) and not is_map_key(tool, "custom") do
    with :ok <- validate_exact_custom_keys(tool, ["type", "name", "description", "format"]) do
      {:ok, tool}
    end
  end

  defp translate_tool(%{"type" => "custom"}),
    do: {:error, Error.invalid_request("custom tool requires nested custom properties", "tools")}

  defp translate_tool(%{"type" => type} = tool)
       when type in ["web_search_preview", "image_generation"],
       do: {:ok, tool}

  defp translate_tool(_tool),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_custom_tool_choice_shape(%{
         "tool_choice" => %{"type" => "custom", "custom" => %{} = custom} = choice
       }) do
    with :ok <- validate_exact_custom_choice_keys(choice, ["type", "custom"]) do
      validate_exact_custom_choice_keys(custom, ["name"])
    end
  end

  defp validate_custom_tool_choice_shape(%{"tool_choice" => %{"type" => "custom"}}),
    do: {:error, Error.invalid_request("tool_choice shape is not translatable", "tool_choice")}

  defp validate_custom_tool_choice_shape(_payload), do: :ok

  defp validate_exact_custom_keys(value, allowed_keys) do
    case value |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] -> :ok
      [_key | _rest] -> {:error, Error.invalid_request("tool shape is not translatable", "tools")}
    end
  end

  defp validate_exact_custom_choice_keys(value, allowed_keys) do
    case value |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] ->
        :ok

      [_key | _rest] ->
        {:error, Error.invalid_request("tool_choice shape is not translatable", "tool_choice")}
    end
  end

  defp maybe_put_tool_choice(acc, %{
         "tool_choice" => %{"type" => "function", "function" => %{"name" => name}}
       })
       when is_binary(name),
       do: Map.put(acc, "tool_choice", %{"type" => "function", "name" => name})

  defp maybe_put_tool_choice(acc, %{
         "tool_choice" => %{"type" => "custom", "custom" => %{"name" => name}}
       })
       when is_binary(name),
       do: Map.put(acc, "tool_choice", %{"type" => "custom", "name" => name})

  defp maybe_put_tool_choice(acc, %{"tool_choice" => tool_choice}),
    do: Map.put(acc, "tool_choice", tool_choice)

  defp maybe_put_tool_choice(acc, _payload), do: acc

  defp put_text_options(acc, payload) do
    with {:ok, acc} <- put_text_format(acc, payload) do
      put_text_verbosity(acc, payload)
    end
  end

  defp put_text_format(acc, %{"response_format" => response_format}) do
    case response_format do
      %{"type" => "json_object"} ->
        {:ok, Map.put(acc, "text", %{"format" => %{"type" => "json_object"}})}

      %{"type" => "json_schema", "json_schema" => schema} when is_map(schema) ->
        {:ok, Map.put(acc, "text", %{"format" => Map.put(schema, "type", "json_schema")})}

      %{"type" => "json_schema"} ->
        {:error, Error.invalid_request("response_format json_schema must be an object", "response_format")}

      %{"type" => "text"} ->
        {:ok, Map.put(acc, "text", %{"format" => %{"type" => "text"}})}

      _format ->
        {:ok, Map.put(acc, "response_format", response_format)}
    end
  end

  defp put_text_format(acc, _payload), do: {:ok, acc}

  defp put_text_verbosity(acc, %{"verbosity" => verbosity}) do
    text = Map.get(acc, "text", %{})
    {:ok, Map.put(acc, "text", Map.put(text, "verbosity", normalize_enum(verbosity)))}
  end

  defp put_text_verbosity(acc, _payload), do: {:ok, acc}

  defp maybe_put_max_output_tokens(acc, %{"max_completion_tokens" => value}),
    do: Map.put(acc, "max_output_tokens", value)

  defp maybe_put_max_output_tokens(acc, %{"max_tokens" => value}),
    do: Map.put(acc, "max_output_tokens", value)

  defp maybe_put_max_output_tokens(acc, _payload), do: acc

  defp maybe_put_reasoning(acc, %{"reasoning_effort" => effort}),
    do: Map.put(acc, "reasoning", %{"effort" => normalize_enum(effort)})

  defp maybe_put_reasoning(acc, _payload), do: acc

  defp maybe_put(acc, source, "service_tier" = key) do
    case Map.fetch(source, key) do
      {:ok, value} when is_binary(value) -> Map.put(acc, key, value)
      {:ok, value} -> Map.put(acc, key, value)
      :error -> acc
    end
  end

  defp maybe_put(acc, source, key) do
    case Map.fetch(source, key) do
      {:ok, value} -> Map.put(acc, key, value)
      :error -> acc
    end
  end

  defp normalize_enum(value) when is_binary(value),
    do: value |> String.trim() |> String.downcase()
end
