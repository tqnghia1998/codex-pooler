defmodule CodexPooler.Gateway.Runtime.Streaming.StreamDispatch do
  @moduledoc """
  Builds and runs downstream stream relays for gateway runtime dispatch.
  """

  alias CodexPooler.Gateway.OperationalSettings
  alias CodexPooler.Gateway.Payloads.CompactionTrigger
  alias CodexPooler.Gateway.Payloads.RequestOptions
  alias CodexPooler.Gateway.Persistence.SessionContinuity
  alias CodexPooler.Gateway.Routing.ModelMetadata
  alias CodexPooler.Gateway.Runtime.Dispatch.ResponseContext
  alias CodexPooler.Gateway.Runtime.Dispatch.RouteState
  alias CodexPooler.Gateway.Runtime.Dispatch.SelectedCandidateContext
  alias CodexPooler.Gateway.Runtime.RateLimitObserver
  alias CodexPooler.Gateway.Runtime.Streaming.CompactionResultCollector
  alias CodexPooler.Gateway.Runtime.Streaming.DownstreamDeliveryEvidence
  alias CodexPooler.Gateway.Runtime.Streaming.DownstreamStream
  alias CodexPooler.Gateway.Runtime.Streaming.OpenAIStreamCollector
  alias CodexPooler.Gateway.Runtime.Streaming.StreamAttempt
  alias CodexPooler.Gateway.Runtime.Streaming.StreamLifecycle
  alias CodexPooler.Gateway.Runtime.Streaming.StreamUsageObserver
  alias CodexPooler.Gateway.Runtime.Streaming.Types, as: StreamTypes
  alias CodexPooler.Gateway.Transports.NativeCodexResponseControl
  alias CodexPooler.Gateway.Transports.Streaming.DeferredStreamRegistry
  alias CodexPooler.Gateway.Transports.Streaming.StreamProtocol
  alias CodexPooler.Gateway.Transports.Streaming.StreamRelay
  alias CodexPooler.Gateway.Transports.Streaming.WebsocketBridgeStream
  alias CodexPooler.Gateway.Transports.Streaming.WebsocketCodec

  @sse_keepalive_frame ": keepalive\n\n"
  # The released Codex client resets its stream idle timer only on a parsed SSE
  # event (`eventsource-stream` drops comments) and ignores an unknown `type`,
  # so this data event is what carries a withheld preamble's liveness to it.
  @sse_keepalive_event ~s(event: keepalive\ndata: {"type":"keepalive"}\n\n)
  @backend_turn_state_relay_endpoints [
    "/backend-api/codex/responses",
    "/backend-api/codex/responses/compact"
  ]

  @type callbacks :: %{
          required(:finalization_callbacks) => StreamLifecycle.finalization_callbacks(),
          optional(:http_first_event_retry) => StreamLifecycle.http_first_event_retry()
        }
  @type stream_dispatch_result :: StreamTypes.stream_dispatch_result()

  @spec streaming_result(Req.Response.t(), SelectedCandidateContext.t(), callbacks()) ::
          stream_dispatch_result()
  def streaming_result(response, %SelectedCandidateContext{} = context, callbacks) do
    finalization_callbacks = Map.fetch!(callbacks, :finalization_callbacks)

    cond do
      CompactionTrigger.streaming_result?(context.request_options) ->
        CompactionResultCollector.collect(response, context, finalization_callbacks)

      OpenAIStreamCollector.collect_image?(context.request_options) ->
        OpenAIStreamCollector.collect_image(response, context, finalization_callbacks)

      OpenAIStreamCollector.collect_response?(context.request_options) ->
        OpenAIStreamCollector.collect_response(response, context, finalization_callbacks)

      true ->
        relay_streaming_result(response, context, callbacks)
    end
  end

  defp relay_streaming_result(response, %SelectedCandidateContext{} = context, callbacks) do
    result = %{
      status: response.status,
      headers: stream_headers(response, context)
    }

    case context.request_options.transport.websocket_writer do
      writer when is_function(writer, 1) ->
        Map.put(
          result,
          :websocket_stream,
          websocket_stream_result(response, writer, context, callbacks)
        )

      _writer ->
        Map.put(result, :stream, stream_result(response, context, callbacks))
    end
  end

  # The deferred closure runs in the connection process after the request is
  # already reserved and the attempt dispatched. Register it so a rollout drain
  # can reach this exact stream; the registration is refcounted, so a
  # first-event retry's nested stream keeps the one token the relay selects on.
  defp stream_result(response, %SelectedCandidateContext{} = context, callbacks) do
    fn conn ->
      response_context = %ResponseContext{context: context, response: response}

      drain_token =
        DeferredStreamRegistry.register(%{
          request_id: context.reserved.request.id,
          attempt_id: attempt_id(context.attempt)
        })

      try do
        result =
          StreamRelay.run(
            stream_relay_state(conn, context, response),
            response,
            response_context
            |> stream_relay_handlers(response, :http_conn, callbacks)
            |> put_drain_token(drain_token)
          )
          |> http_stream_result()

        DeferredStreamRegistry.finish(
          drain_token,
          if(match?({:error, _}, result), do: :failed, else: :completed)
        )

        result
      catch
        kind, reason ->
          DeferredStreamRegistry.finish(drain_token, :failed)
          :erlang.raise(kind, reason, __STACKTRACE__)
      end
    end
  end

  defp put_drain_token(handlers, nil), do: handlers
  defp put_drain_token(handlers, drain_token), do: Map.put(handlers, :drain_token, drain_token)

  defp websocket_stream_result(response, writer, %SelectedCandidateContext{} = context, callbacks) do
    fn ->
      response_context = %ResponseContext{context: context, response: response}

      StreamRelay.run(
        stream_relay_state(:websocket, context, response),
        response,
        stream_relay_handlers(response_context, response, {:websocket, writer}, callbacks)
      )
      |> case do
        {:ok, _state} -> :ok
        {:error, _gateway_error} = error -> error
      end
    end
  end

  defp stream_relay_handlers(
         %ResponseContext{} = response_context,
         _response,
         :http_conn,
         callbacks
       ) do
    response_context
    |> StreamLifecycle.lifecycle_handlers(callbacks,
      first_event_retry: http_first_event_retry(response_context, callbacks)
    )
    |> with_http_delivery_receipt(response_context)
    |> Map.merge(%{
      buffer_telemetry_opts: buffer_telemetry_opts(response_context),
      write_chunk: http_stream_writer(response_context),
      write_keepalive: http_sse_keepalive_writer(response_context.response),
      before_finalize_failure: http_stream_terminal_failure_writer(response_context),
      before_finalize_success: http_stream_terminal_success_hook(response_context),
      keepalive_interval_ms: sse_keepalive_interval_ms(response_context.response)
    })
  end

  defp stream_relay_handlers(
         %ResponseContext{} = response_context,
         _response,
         {:websocket, writer},
         callbacks
       ) do
    response_context
    |> StreamLifecycle.lifecycle_handlers(callbacks,
      first_event_retry: StreamLifecycle.fail_first_event_handler(response_context)
    )
    # D7 surface isolation: absence of `before_finalize_failure` and
    # `before_finalize_success` keys prevents the HTTP synthetic terminal from
    # leaking onto the GET /v1/responses websocket. Do not add either key here.
    |> Map.merge(%{
      buffer_telemetry_opts: buffer_telemetry_opts(response_context),
      keepalive_interval_ms: 0,
      write_keepalive: fn state -> {:ok, state} end,
      write_chunk: websocket_stream_writer(response_context, writer)
    })
  end

  # The relay cannot name its own transport or route class; the request options
  # already carry both, so hand them over and let `BufferTelemetry` derive the
  # tags. Without this a truncated HTTP SSE body is recorded as
  # transport/route_class "unknown" and cannot be attributed.
  defp buffer_telemetry_opts(%ResponseContext{context: %{request_options: request_options}}),
    do: [request_options: request_options]

  # `Finalization.Streaming` replaces the attempt's response metadata
  # wholesale, so the downstream delivery receipt is merged only after either
  # finalizer returned. The relay hands both finalizers the state that carries
  # the write evidence; a downstream write that failed inside the relay loop is
  # only visible here through its `{:chunk, reason}` failure reason, because
  # the relay keeps the pre-write state on that path.
  defp with_http_delivery_receipt(
         %{finalize_success: finalize_success, finalize_failure: finalize_failure} = handlers,
         %ResponseContext{} = response_context
       ) do
    %{
      handlers
      | finalize_success: fn body, state ->
          result = finalize_success.(body, state)
          record_http_delivery_receipt(state, response_context)
          result
        end,
        finalize_failure: fn body, reason, state ->
          result = finalize_failure.(body, reason, state)

          state
          |> mark_http_write_failure(reason)
          |> record_http_delivery_receipt(response_context)

          result
        end
    }
  end

  defp record_http_delivery_receipt(state, %ResponseContext{context: context}) do
    DownstreamDeliveryEvidence.record(state, %{
      request_id: context.reserved.request.id,
      attempt_id: attempt_id(context.attempt),
      codex_session_id: codex_session_id(context.request_options)
    })
  end

  defp attempt_id(%{id: id}) when is_binary(id), do: id
  defp attempt_id(_attempt), do: nil

  defp codex_session_id(%RequestOptions{continuity: %{codex_session: %{id: id}}}), do: id
  defp codex_session_id(_request_options), do: nil

  defp mark_http_write_failure(state, reason) do
    if chunk_write_failure?(reason),
      do: DownstreamDeliveryEvidence.record_write_failure(state),
      else: state
  end

  defp chunk_write_failure?({:chunk, _reason}), do: true

  defp chunk_write_failure?({:upstream_stream_interrupted, reason}),
    do: chunk_write_failure?(reason)

  defp chunk_write_failure?(_reason), do: false

  defp write_downstream_chunk(state, data) do
    case write_downstream_chunk_preserving_state(state, data) do
      {:ok, state} -> {:ok, state}
      {:error, reason, _state} -> {:error, reason}
    end
  end

  defp write_downstream_chunk_preserving_state(state, data) do
    case update_relay_target(state, &Plug.Conn.chunk(&1, data)) do
      {:ok, state} -> {:ok, DownstreamDeliveryEvidence.record_write(state, data)}
      {:error, reason} -> {:error, reason, state}
    end
  end

  defp http_stream_result({:ok, %{target: _target} = state}), do: {:ok, relay_target(state)}
  defp http_stream_result({:ok, _finalized} = result), do: result
  defp http_stream_result({:error, _gateway_error} = error), do: error

  defp websocket_stream_writer(%ResponseContext{context: context} = response_context, writer) do
    request = context.reserved.request

    fn state, data ->
      {data, state, _delivery} =
        normalize_stream_data(response_context, state, data, &visible_websocket_data?/1)

      {messages, websocket_sse_block_state} =
        WebsocketCodec.stream_messages(request, data, websocket_sse_block_state(state))

      Enum.each(messages, writer)

      {:ok, put_websocket_sse_block_state(state, websocket_sse_block_state)}
    end
  end

  defp mark_visible_output(request, attempt) do
    SessionContinuity.mark_codex_turn_visible(request, attempt)
  end

  defp visible_websocket_data?(data), do: is_binary(data) and data != ""

  defp stream_relay_state(
         :websocket = target,
         %SelectedCandidateContext{request_options: %RequestOptions{} = opts},
         response
       ) do
    target
    |> base_stream_relay_state(opts, response)
    |> put_first_event_state(StreamAttempt.first_event_state())
    |> put_rate_limit_state(RateLimitObserver.event_state())
    |> put_usage_state(StreamUsageObserver.new())
    |> Map.put(:websocket_sse_block_state, StreamProtocol.new_sse_block_state())
  end

  defp stream_relay_state(
         target,
         %SelectedCandidateContext{
           request_options: %RequestOptions{} = opts,
           reserved: %{request: request}
         },
         response
       ) do
    target
    |> base_stream_relay_state(opts, response)
    |> maybe_enable_native_http_progress(request)
    |> maybe_enable_native_http_tool_observation(request)
    |> put_first_event_state(StreamAttempt.first_event_state())
    |> put_rate_limit_state(RateLimitObserver.event_state())
    |> put_usage_state(StreamUsageObserver.new())
  end

  defp maybe_enable_native_http_progress(
         state,
         %{request_metadata: %{"native_http_claim_arm" => "post_compaction_resume"}}
       ),
       do: DownstreamStream.enable_native_http_progress(state)

  defp maybe_enable_native_http_progress(state, _request), do: state

  defp maybe_enable_native_http_tool_observation(
         %{target: %Plug.Conn{}} = state,
         %{transport: "http_sse", native_client_retry_version: 1, request_metadata: %{"native_http_claim_arm" => arm}}
       )
       when arm in ["opening", "tool_continuation"],
       do: DownstreamStream.enable_native_http_tool_observation(state)

  defp maybe_enable_native_http_tool_observation(state, _request), do: state

  defp base_stream_relay_state(target, %RequestOptions{} = opts, response) do
    DownstreamStream.initial_state(target, opts, stream_source(response))
    |> Map.put(:rate_limit_identity, stream_rate_limit_identity(response))
  end

  defp stream_rate_limit_identity(%Req.Response{body: %WebsocketBridgeStream{}}), do: nil
  defp stream_rate_limit_identity(%Req.Response{}), do: :selected

  @spec stream_source(Req.Response.t()) :: DownstreamStream.source()
  defp stream_source(%Req.Response{body: %WebsocketBridgeStream{}}), do: :websocket_bridge
  defp stream_source(%Req.Response{}), do: :http

  defp relay_target(%{target: target}), do: target

  defp first_event_state(%{first_event: %{} = state}), do: state

  defp put_first_event_state(%{} = state, %{} = first_event_state),
    do: Map.put(state, :first_event, first_event_state)

  defp rate_limit_state(%{
         rate_limit: %{buffer: buffer, skip_leading_lf?: skip_leading_lf?} = state
       })
       when is_binary(buffer) and is_boolean(skip_leading_lf?),
       do: state

  defp put_rate_limit_state(
         %{} = state,
         %{buffer: buffer, skip_leading_lf?: skip_leading_lf?} = rate_limit_state
       )
       when is_binary(buffer) and is_boolean(skip_leading_lf?),
       do: Map.put(state, :rate_limit, rate_limit_state)

  defp usage_state(%{usage_observer: %{} = usage_state}), do: usage_state
  defp usage_state(_state), do: StreamUsageObserver.new()

  defp put_usage_state(%{} = state, %{} = usage_state),
    do: Map.put(state, :usage_observer, usage_state)

  defp websocket_sse_block_state(%{websocket_sse_block_state: %{buffer: buffer} = state})
       when is_binary(buffer),
       do: state

  defp websocket_sse_block_state(_state), do: StreamProtocol.new_sse_block_state()

  defp put_websocket_sse_block_state(%{} = state, %{buffer: buffer} = sse_block_state)
       when is_binary(buffer),
       do: Map.put(state, :websocket_sse_block_state, sse_block_state)

  defp update_relay_target(%{target: target} = state, fun) when is_function(fun, 1) do
    case fun.(target) do
      {:ok, target} -> {:ok, %{state | target: target}}
      {:error, _reason} = error -> error
    end
  end

  # Compact streams classify like every other SSE stream — a compact terminal
  # failure must finalize as a failure, never as a relayed success. Compactness
  # only changes what happens on a retryable first event, and that decision
  # lives in StreamLifecycle.first_event_retry_handler (compact model misses
  # finalize without retry or health mutation).
  defp http_stream_writer(%ResponseContext{response: response} = response_context) do
    sse_response? = sse_response?(response)

    assignment_source? =
      sse_response? and
        ModelMetadata.assignment_source?(
          response_context.context.model,
          response_context.context.assignment.id
        )

    fn conn, data ->
      if sse_response? do
        previous_state = first_event_state(conn)

        {classification, first_event_state} =
          StreamAttempt.classify_first_event(
            data,
            previous_state,
            assignment_source?
          )

        conn = put_first_event_state(conn, first_event_state)

        classification
        |> attach_withheld_body(previous_state, data)
        |> handle_classified_stream_data(response_context, conn, data)
      else
        write_stream_data(response_context, conn, data)
      end
    end
  end

  # The relay retains every streamed part — including non-visible blocks that
  # were already written downstream — so the exhaustion path must not replay
  # the whole retained body. The classifier's buffer plus the intercepted chunk
  # is exactly the content the client has not received yet; carry it on the
  # failure so final delivery writes only that.
  defp attach_withheld_body({:retry, failure}, %{buffer: buffer}, data),
    do: {:retry, Map.put(failure, :withheld_body, buffer <> data)}

  defp attach_withheld_body(classification, _previous_state, _data), do: classification

  # A retryable terminal may arrive after lifecycle or response metadata, but
  # before the provider has committed model output to the client. Those records
  # carry candidate-specific response ids, model headers, verification,
  # moderation, safety, and turn-state state. Retain them on the downstream
  # connection until this attempt commits. A retry discards the failed
  # candidate's bytes; the successful attempt flushes its own exactly once.
  @withheld_preamble :codex_pooler_withheld_retry_preamble
  # Set when a preamble block is withheld and cleared once a keepalive event
  # has told the client about it (or the preamble is flushed or discarded), so
  # the client only ever hears the provider's own liveness, at most one
  # keepalive interval late, and never the Pooler's.
  @withheld_preamble_unannounced :codex_pooler_withheld_preamble_unannounced

  defp withheld_preamble(%{target: %Plug.Conn{private: private}}),
    do: Map.get(private, @withheld_preamble, "")

  defp withhold_preamble(%{target: %Plug.Conn{} = target} = state, preamble)
       when is_binary(preamble) and preamble != "" do
    target =
      target
      |> Plug.Conn.put_private(@withheld_preamble, withheld_preamble(state) <> preamble)
      |> Plug.Conn.put_private(@withheld_preamble_unannounced, true)

    %{state | target: target}
  end

  defp withhold_preamble(state, _preamble), do: state

  defp take_withheld_preamble(%{target: %Plug.Conn{}} = state) do
    {withheld_preamble(state), clear_withheld_preamble(state)}
  end

  defp take_withheld_preamble(state), do: {"", state}

  defp discard_withheld_preamble(%{target: %Plug.Conn{}} = state), do: clear_withheld_preamble(state)
  defp discard_withheld_preamble(state), do: state

  defp clear_withheld_preamble(%{target: %Plug.Conn{private: private} = target} = state),
    do: %{state | target: %{target | private: Map.drop(private, [@withheld_preamble, @withheld_preamble_unannounced])}}

  defp unannounced_withheld_preamble?(%{target: %Plug.Conn{private: private}}),
    do: Map.get(private, @withheld_preamble_unannounced, false)

  defp unannounced_withheld_preamble?(_state), do: false

  defp announce_withheld_preamble(%{target: %Plug.Conn{} = target} = state),
    do: %{state | target: Plug.Conn.put_private(target, @withheld_preamble_unannounced, false)}

  # The first-event classifier can hold a large first event until the stream
  # ends, so both finalize hooks must flush the held bytes through the normal
  # write path first — a structurally complete trailing terminal without a
  # final separator is only recoverable from that buffer.
  defp http_stream_terminal_failure_writer(%ResponseContext{} = response_context) do
    fn state, reason ->
      state = mark_http_write_failure(state, reason)

      case flush_buffered_first_event(response_context, state) do
        {:ok, state} ->
          finalize_http_stream_failure(state, reason)

        {:chunk_error, state, _chunk_reason} ->
          state
          |> DownstreamDeliveryEvidence.record_write_failure()
          |> finalize_http_stream_failure(reason)
      end
    end
  end

  defp finalize_http_stream_failure(state, reason) do
    case {DownstreamStream.terminal_outcome(state), reason} do
      {terminal, _reason} when terminal in [:completed, :incomplete] ->
        {:success, state, ""}

      {{:failed, _failure}, {:terminal_stream_failure, _existing_failure}} ->
        {:failure, state, "", reason}

      {{:failed, %{} = failure}, _reason} ->
        {:failure, state, "", {:terminal_stream_failure, failure}}

      {{:failed, _failure}, _reason} ->
        {:failure, state, "", reason}

      {_missing_terminal, _reason} ->
        http_stream_missing_terminal_failure_result(state, reason)
    end
  end

  defp http_stream_missing_terminal_failure_result(state, reason) do
    tagged_reason = DownstreamStream.terminal_missing_interruption_reason(state, reason)

    case write_public_openai_responses_terminal_failure(state, reason) do
      {:ok, state, ""} when is_tuple(tagged_reason) -> {:failure, state, "", tagged_reason}
      {:ok, state, ""} -> {:ok, state, ""}
      {:ok, state, data} -> {:failure, state, data, tagged_reason}
      {:error, _reason} = error -> error
    end
  end

  defp http_stream_terminal_success_hook(%ResponseContext{} = response_context) do
    fn state ->
      case flush_buffered_first_event(response_context, state) do
        {:ok, state} ->
          finalize_http_stream_success(state)

        {:chunk_error, state, reason} ->
          state
          |> DownstreamDeliveryEvidence.record_write_failure()
          |> finalize_flushed_chunk_error(reason)
      end
    end
  end

  defp finalize_http_stream_success(state) do
    case DownstreamStream.terminal_outcome(state) do
      {:failed, %{} = failure} ->
        {:failure, state, "", {:terminal_stream_failure, failure}}

      {:failed, _failure} ->
        {:failure, state, "", :upstream_stream_interrupted}

      terminal when terminal in [:completed, :incomplete] ->
        {:ok, state, ""}

      _missing_terminal ->
        if DownstreamStream.native_http_tool_started?(state),
          do: {:failure, state, "", :upstream_stream_interrupted},
          else: missing_public_openai_responses_terminal_result(state)
    end
  end

  defp missing_public_openai_responses_terminal_result(state) do
    case write_public_openai_responses_terminal_failure(state, :upstream_stream_interrupted) do
      {:ok, state, ""} -> {:ok, state, ""}
      {:ok, state, data} -> {:failure, state, data, :upstream_stream_interrupted}
      {:error, _reason} -> {:failure, state, "", :upstream_stream_interrupted}
    end
  end

  defp write_public_openai_responses_terminal_failure(state, reason) do
    case DownstreamStream.synthetic_terminal_failure(state, reason) do
      {nil, state} ->
        {:ok, state, ""}

      {data, state} ->
        # D6 hazard 2: Synthetic bytes must go straight to Plug.Conn.chunk/2,
        # never through normalize_block/2, because their own server_error frame
        # would canonicalize back to response.failed.
        case write_downstream_chunk(state, data) do
          {:ok, state} -> {:ok, state, data}
          {:error, _reason} = error -> error
        end
    end
  end

  # A flush-time chunk error must keep both the advanced parser/usage state
  # and the original write reason: `{:chunk, reason}` is the canonical
  # downstream-write failure shape the finalization layer classifies (for
  # example `{:chunk, :closed}` becomes client_disconnected).
  defp flush_buffered_first_event(%ResponseContext{} = response_context, state) do
    case first_event_state(state) do
      %{buffer: ""} ->
        write_eof_normalized_stream_data(response_context, state)

      %{buffer: buffer} = first_event ->
        state = put_first_event_state(state, %{first_event | buffer: ""})

        write_flushed_first_event(
          response_context,
          state,
          terminate_complete_sse_block_at_eof(buffer)
        )
    end
  end

  # A first-event terminal can be structurally complete when the upstream EOF
  # supplies the only missing SSE blank line. The ordinary relay receives that
  # separator in a later chunk; at EOF, add it only when the entire buffer
  # becomes complete, then send it through the same normalizer and preamble
  # gate as every other downstream write.
  defp terminate_complete_sse_block_at_eof(buffer) do
    terminated = buffer <> "\n\n"

    with {[_block], ""} <- StreamProtocol.complete_sse_blocks(terminated, bounded?: false),
         {:ok, %{kind: kind}} <- StreamProtocol.terminal_outcome(terminated),
         true <- kind in [:completed, :incomplete, :failed] do
      terminated
    else
      _incomplete_or_nonterminal -> buffer
    end
  end

  defp write_flushed_first_event(%ResponseContext{} = response_context, state, buffer) do
    case write_stream_data_preserving_state(response_context, state, buffer) do
      {:ok, state} -> write_eof_normalized_stream_data(response_context, state)
      {:error, reason, state} -> {:chunk_error, state, reason}
    end
  end

  defp write_eof_normalized_stream_data(
         %ResponseContext{context: %{payload: payload, request_options: opts}},
         state
       ) do
    {data, state, delivery} =
      DownstreamStream.flush_eof_delivery(DownstreamStream.endpoint(payload, opts), opts, state)

    case write_normalized_stream_data_preserving_state(state, data, delivery) do
      {:ok, state} -> {:ok, state}
      {:error, reason, state} -> {:chunk_error, state, reason}
    end
  end

  # Mirrors finalize_http_stream_failure precedence for a flush that parsed
  # data but could not write it: an upstream terminal decoded from the flushed
  # buffer still settles the turn, and only a terminal-less flush classifies as
  # the downstream write failure. No synthetic terminal is attempted because
  # the downstream connection is already gone.
  defp finalize_flushed_chunk_error(state, reason) do
    case DownstreamStream.terminal_outcome(state) do
      terminal when terminal in [:completed, :incomplete] ->
        {:ok, state, ""}

      {:failed, %{} = failure} ->
        {:failure, state, "", {:terminal_stream_failure, failure}}

      _other ->
        {:failure, state, "", {:chunk, reason}}
    end
  end

  defp http_sse_keepalive_writer(response) do
    if sse_response?(response) do
      &write_sse_keepalive/1
    else
      fn conn -> {:ok, conn} end
    end
  end

  # While a candidate's preamble is withheld the client receives none of the
  # provider's events, so an event-level idle timer (Codex's 300 s stream idle
  # timeout) can fire although the provider is streaming. On the native Codex
  # routes only, the next keepalive after a withheld preamble block is written
  # as a data event instead of the comment: it carries no candidate state, so
  # a first-event retry stays invisible. Silence after that stays comments, so
  # the client's timer still measures the provider, as it would directly, and
  # nothing but comments follows a terminal. The public `/v1` surfaces keep
  # comments: their SDK idle timers are byte-level and a `keepalive` there
  # would need the public sequence numbering.
  defp write_sse_keepalive(state) do
    cond do
      not keepalive_allowed?(state) ->
        {:ok, state}

      announce_keepalive_event?(state) ->
        state
        |> announce_withheld_preamble()
        |> update_relay_target(&Plug.Conn.chunk(&1, @sse_keepalive_event))

      true ->
        update_relay_target(state, &Plug.Conn.chunk(&1, @sse_keepalive_frame))
    end
  end

  defp announce_keepalive_event?(state) do
    unannounced_withheld_preamble?(state) and not public_stream_state?(state) and
      is_nil(DownstreamStream.terminal_outcome(state))
  end

  defp public_stream_state?(state),
    do: Map.has_key?(state, :public_openai_responses) or Map.has_key?(state, :public_openai_chat)

  defp keepalive_allowed?(state) do
    first_event_state(state).buffer == "" and DownstreamStream.keepalive_allowed?(state)
  end

  defp sse_keepalive_interval_ms(response) do
    if sse_response?(response),
      do: OperationalSettings.current().sse_keepalive_interval_ms,
      else: 0
  end

  defp handle_classified_stream_data(
         {:retry, failure},
         _response_context,
         _conn,
         _data
       ),
       do: {:retry_first_event, failure}

  defp handle_classified_stream_data(
         {:write, data},
         response_context,
         conn,
         _input
       ),
       do: write_stream_data(response_context, conn, data)

  defp handle_classified_stream_data(
         {:write_terminal_failure, data, failure},
         response_context,
         conn,
         _input
       ) do
    case write_stream_data(response_context, conn, data) do
      {:ok, conn} -> {:terminal_stream_failure, conn, failure}
      {:error, _reason} = error -> error
    end
  end

  defp handle_classified_stream_data(
         :buffered,
         _response_context,
         conn,
         _data
       ),
       do: {:ok, conn}

  defp sse_response?(response) do
    response
    |> header("content-type")
    |> Kernel.||("text/event-stream")
    |> String.contains?("text/event-stream")
  end

  defp write_stream_data(%ResponseContext{} = response_context, conn, data) do
    case write_stream_data_preserving_state(response_context, conn, data) do
      {:ok, conn} -> {:ok, conn}
      {:error, reason, _conn} -> {:error, reason}
    end
  end

  # The turn becomes visible on the first chunk that shows the client output or
  # a terminal. A lifecycle preamble is withheld for the first-event retry
  # window and never reaches the client on its own, so it must not stamp the
  # turn: the Pooler's own receive timeout after it left the client's resend
  # refused as a duplicate of output it was never shown (findings#225 row
  # 225-191).
  defp write_stream_data_preserving_state(%ResponseContext{} = response_context, conn, data) do
    {downstream_data, conn, delivery} =
      normalize_stream_data(response_context, conn, data, &StreamProtocol.stream_data_client_visible?/1)

    write_normalized_stream_data_preserving_state(conn, downstream_data, delivery)
  end

  defp write_normalized_stream_data_preserving_state(conn, downstream_data, nil) do
    {preamble, downstream_data, _preamble_seen?} =
      StreamProtocol.partition_preamble_blocks(downstream_data)

    write_normalized_stream_data_preserving_state(conn, downstream_data, %{
      preamble: preamble,
      data: downstream_data,
      commits?: downstream_data != "" and commits_withheld_preamble?(downstream_data)
    })
  end

  defp write_normalized_stream_data_preserving_state(conn, _data, %{
         preamble: preamble,
         data: downstream_data,
         commits?: commits?
       }) do
    conn = withhold_preamble(conn, preamble)

    cond do
      downstream_data == "" ->
        {:ok, conn}

      commits? ->
        {preamble, conn} = take_withheld_preamble(conn)
        write_normalized_chunk_and_commit_progress(conn, preamble <> downstream_data)

      true ->
        write_normalized_chunk_and_commit_progress(conn, downstream_data)
    end
  end

  defp write_normalized_chunk_and_commit_progress(state, data) do
    case write_downstream_chunk_preserving_state(state, data) do
      {:ok, state} -> {:ok, DownstreamStream.commit_native_http_progress(state)}
      {:error, reason, state} -> {:error, reason, state}
    end
  end

  defp commits_withheld_preamble?(data) do
    StreamProtocol.stream_data_visible?(data) or
      match?(
        {:ok, %{kind: kind}} when kind in [:completed, :incomplete, :failed],
        StreamProtocol.terminal_outcome(data)
      )
  end

  defp reset_first_event_retry_state(conn) do
    conn
    |> discard_withheld_preamble()
    |> put_first_event_state(StreamAttempt.first_event_state())
    |> put_rate_limit_state(RateLimitObserver.event_state())
    |> put_usage_state(StreamUsageObserver.new())
  end

  defp http_first_event_retry(%ResponseContext{} = response_context, callbacks) do
    callbacks
    |> Map.fetch!(:http_first_event_retry)
    |> then(fn retry ->
      retry.(response_context,
        retry_allowed?: not candidate_specific_http_headers_committed?(response_context),
        reset_state: &reset_first_event_retry_state/1,
        write_final_event: &write_final_first_event(response_context, &1, &2),
        stream_candidate: &stream_candidate_result/2
      )
    end)
  end

  defp candidate_specific_http_headers_committed?(%ResponseContext{
         context: context,
         response: response
       }) do
    candidate_headers = [
      "openai-model",
      "x-reasoning-included",
      "x-codex-safety-buffering-enabled",
      "x-codex-safety-buffering-faster-model",
      "x-codex-turn-state"
    ]

    response
    |> stream_headers(context)
    |> Enum.any?(fn {name, _value} ->
      name in candidate_headers
    end)
  end

  # The last-candidate first-event failure finalizes the attempt before this
  # write, so its receipt is recorded here rather than by the wrapped
  # finalizers.
  defp write_final_first_event(response_context, conn, data) do
    case write_stream_data(response_context, conn, data) do
      {:ok, conn} ->
        record_http_delivery_receipt(conn, response_context)
        {:ok, conn}

      {:error, _reason} = error ->
        conn
        |> DownstreamDeliveryEvidence.record_write_failure()
        |> record_http_delivery_receipt(response_context)

        error
    end
  end

  defp stream_candidate_result({:retry, nil}, conn), do: {:ok, conn}
  defp stream_candidate_result({:retry, reason}, _conn), do: {:error, reason}

  defp stream_candidate_result({:ok, %{stream: stream}}, conn) do
    case stream.(relay_target(conn)) do
      {:ok, %Plug.Conn{} = target} -> {:ok, %{conn | target: target}}
      {:ok, _finalized} -> {:ok, conn}
      {:error, _gateway_error} = error -> error
    end
  end

  defp stream_candidate_result({:ok, %{websocket_stream: stream}}, conn) do
    case stream.() do
      :ok -> {:ok, conn}
      {:error, _gateway_error} = error -> error
    end
  end

  defp stream_candidate_result({:ok, %{raw_body: body}}, conn) when is_binary(body),
    do: update_relay_target(conn, &Plug.Conn.chunk(&1, body))

  defp stream_candidate_result({:ok, %{body: body}}, conn) when is_map(body),
    do: update_relay_target(conn, &Plug.Conn.chunk(&1, CodexPooler.JSON.encode!(body)))

  defp stream_candidate_result({:ok, _result}, conn), do: {:ok, conn}
  defp stream_candidate_result({:error, reason}, _conn), do: {:error, reason}

  defp normalize_stream_data(
         %ResponseContext{context: context},
         state,
         data,
         visible_data?
       )
       when is_function(visible_data?, 1) do
    %{reserved: reserved, payload: payload, request_options: opts} = context

    {:ok, rate_limit_state} =
      case Map.get(state, :rate_limit_identity) do
        nil -> {:ok, rate_limit_state(state)}
        _identity -> RateLimitObserver.collect_events(data, rate_limit_state(state))
      end

    state = put_rate_limit_state(state, rate_limit_state)

    state = put_usage_state(state, StreamUsageObserver.observe(usage_state(state), data))

    case maybe_mark_visible_output(state, reserved.request, context.attempt, data, visible_data?) do
      {:ok, state} ->
        DownstreamStream.normalize_delivery(
          data,
          DownstreamStream.endpoint(payload, opts),
          opts,
          state
        )

      {:error, :stale_generation, state} ->
        {"", state, nil}
    end
  end

  defp maybe_mark_visible_output(
         %{visible_output_marked?: true} = state,
         _request,
         _attempt,
         _data,
         _visible_data?
       ),
       do: {:ok, state}

  defp maybe_mark_visible_output(state, request, attempt, data, visible_data?) do
    if visible_data?.(data) do
      case mark_visible_output(request, attempt) do
        :ok -> {:ok, Map.put(state, :visible_output_marked?, true)}
        {:error, :stale_generation} -> {:error, :stale_generation, state}
      end
    else
      {:ok, state}
    end
  end

  defp stream_headers(response, %SelectedCandidateContext{} = context) do
    content_type = header(response, "content-type") || "text/event-stream"

    [{"cache-control", "no-cache"}, {"content-type", content_type}]
    |> maybe_put_backend_turn_state_response_header(response, context.request_options)
    |> maybe_put_native_response_control_headers(response, context.request_options)
    |> maybe_put_backend_models_etag(context)
  end

  defp maybe_put_native_response_control_headers(
         headers,
         response,
         %RequestOptions{
           transport: %{
             transport: "http_sse",
             upstream_endpoint: "/backend-api/codex/responses",
             websocket_writer: nil
           },
           openai_compatibility: %{source_endpoint: nil, openai_chat_payload: nil}
         }
       ) do
    NativeCodexResponseControl.http_headers(Req.Response.to_map(response).headers) ++ headers
  end

  defp maybe_put_native_response_control_headers(headers, _response, _request_options),
    do: headers

  defp maybe_put_backend_models_etag(
         headers,
         %SelectedCandidateContext{
           route_state: %RouteState{} = route_state,
           request_options: %RequestOptions{
             transport: %{
               transport: "http_sse",
               upstream_endpoint: "/backend-api/codex/responses",
               websocket_writer: nil
             },
             openai_compatibility: %{source_endpoint: nil, openai_chat_payload: nil}
           }
         }
       ) do
    case RouteState.codex_models_etag(route_state) do
      etag when is_binary(etag) -> [{"x-models-etag", etag} | headers]
      _etag -> headers
    end
  end

  defp maybe_put_backend_models_etag(headers, _context), do: headers

  defp maybe_put_backend_turn_state_response_header(
         headers,
         response,
         %RequestOptions{
           transport: %{upstream_endpoint: endpoint},
           openai_compatibility: %{source_endpoint: nil, openai_chat_payload: nil}
         }
       )
       when endpoint in @backend_turn_state_relay_endpoints do
    case header(response, "x-codex-turn-state") do
      value when is_binary(value) -> [{"x-codex-turn-state", value} | headers]
      _value -> headers
    end
  end

  defp maybe_put_backend_turn_state_response_header(headers, _response, _request_options) do
    headers
  end

  defp header(%Req.Response{headers: headers}, key) do
    headers
    |> Enum.find_value(fn {name, values} ->
      if String.downcase(name) == key, do: List.first(values)
    end)
  end

  defp header(headers, key) when is_list(headers) do
    headers
    |> Enum.find_value(fn {name, value} ->
      if String.downcase(to_string(name)) == key, do: to_string(value)
    end)
  end
end
