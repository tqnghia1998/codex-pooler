defmodule CodexPoolerWeb.Runtime.BackendFileRoutingTest do
  use CodexPoolerWeb.ConnCase, async: false

  alias CodexPooler.Upstreams.Quota.Windows, as: QuotaWindows

  import Ecto.Query
  import CodexPooler.PoolerFixtures
  import CodexPoolerWeb.Runtime.BackendCodexTestSupport, only: [auth: 2, start_upstream: 1]

  alias CodexPooler.Accounting.Request
  alias CodexPooler.FakeUpstream
  alias CodexPooler.Files
  alias CodexPooler.Files.FileRecord

  alias CodexPooler.Gateway.Persistence.{
    BridgeDemotion,
    RoutingCircuitState
  }

  alias CodexPooler.Gateway.Transports.FileBridge
  alias CodexPooler.Repo
  alias CodexPooler.Upstreams.CodexClientIdentity

  # Failure-detection budget for an expected message: a green run returns as
  # soon as the message arrives, so only a missing one spends it.
  @detection_timeout_ms 15_000

  setup do
    old_config = Application.get_env(:codex_pooler, Files, [])
    CodexPooler.TestAppEnv.restore_on_exit(FileBridge)

    Application.put_env(:codex_pooler, Files,
      max_file_size_bytes: 64,
      file_ttl_seconds: 60
    )

    Application.put_env(:codex_pooler, FileBridge,
      finalize_retry_timeout_ms: 1_000,
      finalize_retry_interval_ms: 0
    )

    on_exit(fn -> Application.put_env(:codex_pooler, Files, old_config) end)

    :ok
  end

  @tag :upstream_file_create_bridge
  @tag :json_upstream_bridge_happy_path
  test "bridges JSON create and finalize through the selected upstream assignment", %{conn: conn} do
    setup = active_api_key_fixture()

    upstream =
      start_upstream(
        FakeUpstream.file_protocol_finalize_retry(
          file_id: "file_upstream_bridge",
          file_name: "bridge-fixture.txt",
          mime_type: "text/plain"
        )
      )

    upstream_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_bridge_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(upstream)},
        access_token: "file-bridge-token"
      })

    secondary_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_upstream_bridge_secondary",
          file_name: "bridge-fixture-secondary.txt",
          mime_type: "text/plain"
        )
      )

    active_upstream_assignment_fixture(setup.pool, %{
      chatgpt_account_id: "acct_file_bridge_secondary_#{System.unique_integer([:positive])}",
      metadata: %{"base_url" => FakeUpstream.url(secondary_upstream)},
      access_token: "file-bridge-secondary-token"
    })

    reset_at = DateTime.add(DateTime.utc_now(), 900, :second) |> DateTime.truncate(:second)

    assert {:ok, [_window]} =
             QuotaWindows.upsert_quota_windows(
               upstream_assignment.identity,
               [
                 %{
                   window_kind: "primary",
                   window_minutes: 300,
                   used_percent: Decimal.new("1"),
                   reset_at: reset_at,
                   source: "codex_response_headers",
                   source_precision: "observed",
                   freshness_state: "fresh"
                 }
               ]
             )

    lineage_metadata =
      CodexPooler.JSON.encode!(%{"forked_from_thread_id" => "file-bridge-lineage"})

    create_conn =
      conn
      |> auth(setup)
      |> put_req_header("user-agent", "codex-test-agent")
      |> put_req_header("x-openai-client", "codex-cli")
      |> put_req_header("x-openai-extra-file-bridge", "broad-openai-file-bridge")
      |> put_req_header("x-openai-subagent", "file-bridge-subagent")
      |> put_req_header("x-openai-memgen-request", "true")
      |> put_req_header("x-codex-guardian", "classifier")
      |> put_req_header("x-codex-inference-call-id", "file-bridge-inference-call")
      |> put_req_header("x-codex-turn-state", "safe-turn-state")
      |> put_req_header("x-codex-turn-metadata", lineage_metadata)
      |> put_req_header("x-codex-parent-thread-id", "file-bridge-parent-thread")
      |> put_req_header("x-codex-extra-file-bridge", "broad-codex-file-bridge")
      |> put_req_header("x-ignore-this", "not-forwarded")
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "bridge-fixture.txt",
        "file_size" => 21
      })

    assert %{
             "file_id" => "file_upstream_bridge",
             "upload_url" => "https://fake-upload.invalid/upload/file_upstream_bridge?sig=fake-upload"
           } = json_response(create_conn, 200)

    file = Repo.get_by!(FileRecord, file_id: "file_upstream_bridge")
    assert file.pool_id == setup.pool.id
    assert file.api_key_id == setup.api_key.id
    assert file.pool_upstream_assignment_id == upstream_assignment.assignment.id
    assert file.upstream_identity_id == upstream_assignment.identity.id
    assert file.status == "pending_upload"
    assert file.finalize_status == "pending"
    assert file.filename == "bridge-fixture.txt"
    assert file.byte_size == 21
    refute inspect(file) =~ "fake-upload.invalid"

    finalize_conn =
      build_conn()
      |> auth(setup)
      |> post(~p"/backend-api/files/file_upstream_bridge/uploaded", %{})

    assert %{
             "status" => "success",
             "download_url" => "https://fake-download.invalid/download/file_upstream_bridge?sig=fake-download",
             "file_name" => "bridge-fixture.txt",
             "mime_type" => "text/plain"
           } = json_response(finalize_conn, 200)

    finalized_file = Repo.get!(FileRecord, file.id)
    assert finalized_file.status == "uploaded"
    assert finalized_file.finalize_status == "succeeded"
    refute inspect(finalized_file) =~ "fake-download.invalid"

    assert [create_request, first_finalize_request, second_finalize_request] =
             FakeUpstream.requests(upstream)

    assert create_request.path == "/backend-api/files"

    assert create_request.json == %{
             "file_name" => "bridge-fixture.txt",
             "file_size" => 21,
             "use_case" => "codex"
           }

    assert first_finalize_request.path == "/backend-api/files/file_upstream_bridge/uploaded"
    assert second_finalize_request.path == "/backend-api/files/file_upstream_bridge/uploaded"
    assert first_finalize_request.json == %{}
    assert second_finalize_request.json == %{}
    assert FakeUpstream.requests(secondary_upstream) == []

    assert header!(create_request.headers, "authorization") == "Bearer file-bridge-token"
    assert header!(create_request.headers, "accept") == "application/json"
    assert header!(create_request.headers, "content-type") == "application/json"

    assert header!(create_request.headers, "chatgpt-account-id") ==
             upstream_assignment.identity.chatgpt_account_id

    assert header!(create_request.headers, "user-agent") ==
             "codex_cli_rs/#{CodexClientIdentity.version()}"

    assert header!(create_request.headers, "originator") == CodexClientIdentity.originator()
    assert header!(create_request.headers, "version") == CodexClientIdentity.version()
    assert header!(create_request.headers, "x-openai-subagent") == "file-bridge-subagent"
    assert header!(create_request.headers, "x-openai-memgen-request") == "true"
    assert header!(create_request.headers, "x-codex-guardian") == "classifier"
    assert header!(create_request.headers, "x-codex-inference-call-id") == "file-bridge-inference-call"
    assert header!(create_request.headers, "x-codex-turn-state") == "safe-turn-state"
    assert header!(create_request.headers, "x-codex-turn-metadata") == lineage_metadata

    assert header!(create_request.headers, "x-codex-parent-thread-id") ==
             "file-bridge-parent-thread"

    # The files bridge takes the native Responses allowlist (findings#240):
    # a prefix alone no longer forwards a header.
    for name <- ["x-openai-client", "x-openai-extra-file-bridge", "x-codex-extra-file-bridge", "x-ignore-this"] do
      refute Enum.any?(create_request.headers, fn {header_name, _value} -> header_name == name end)
    end

    refute inspect(create_request.headers) =~ "broad-openai-file-bridge"
    refute inspect(create_request.headers) =~ "broad-codex-file-bridge"

    persistence_text =
      inspect(%{
        file: Repo.get!(FileRecord, file.id),
        requests: Repo.all(from request in Request, where: request.pool_id == ^setup.pool.id)
      })

    refute persistence_text =~ lineage_metadata
    refute persistence_text =~ "file-bridge-lineage"
    refute persistence_text =~ "file-bridge-parent-thread"
    refute persistence_text =~ "broad-openai-file-bridge"
    refute persistence_text =~ "broad-codex-file-bridge"
  end

  # findings#240: the files bridge shares the native Responses metadata
  # allowlist and value bounds, so a client routing hint, beta feature key,
  # out-of-vocabulary guardian value, overlong inference call id, non-true
  # memgen flag or the header form of the installation id never reaches the
  # upstream files endpoint, while the allowlisted names and the bounded
  # provider session headers still do.
  @tag :upstream_file_create_bridge
  test "file bridge create bounds client metadata headers to the shared native allowlist", %{conn: conn} do
    setup = active_api_key_fixture()

    upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_upstream_bounded_headers",
          file_name: "bounded-headers.txt",
          mime_type: "text/plain"
        )
      )

    active_upstream_assignment_fixture(setup.pool, %{
      chatgpt_account_id: "acct_file_bounded_headers_#{System.unique_integer([:positive])}",
      metadata: %{"base_url" => FakeUpstream.url(upstream)},
      access_token: "file-bounded-headers-token"
    })

    overlong_call_id = String.duplicate("f", 129)

    create_conn =
      conn
      |> auth(setup)
      |> put_req_header("x-codex-routing-hint", "model=forged-file-bridge")
      |> put_req_header("x-codex-beta-features", "file-bridge-beta-key")
      |> put_req_header("x-codex-guardian", "auditor")
      |> put_req_header("x-codex-inference-call-id", overlong_call_id)
      |> put_req_header("x-openai-memgen-request", "false")
      |> put_req_header("x-codex-installation-id", "file-bridge-installation")
      |> put_req_header("x-client-request-id", "spaced request id")
      |> put_req_header("x-codex-window-id", "file-bridge-window")
      |> put_req_header("session-id", "file-bridge-session")
      |> put_req_header("thread-id", "file-bridge-thread")
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "bounded-headers.txt",
        "file_size" => 21
      })

    assert %{"file_id" => "file_upstream_bounded_headers"} = json_response(create_conn, 200)

    assert [create_request] = FakeUpstream.requests(upstream)
    assert create_request.path == "/backend-api/files"

    for name <- [
          "x-codex-routing-hint",
          "x-codex-beta-features",
          "x-codex-guardian",
          "x-codex-inference-call-id",
          "x-openai-memgen-request",
          "x-codex-installation-id",
          "x-client-request-id"
        ] do
      refute Enum.any?(create_request.headers, fn {header_name, _value} -> header_name == name end)
    end

    refute inspect(create_request.headers) =~ "forged-file-bridge"
    refute inspect(create_request.headers) =~ "file-bridge-beta-key"
    refute inspect(create_request.headers) =~ "auditor"
    refute inspect(create_request.headers) =~ overlong_call_id
    refute inspect(create_request.headers) =~ "file-bridge-installation"

    assert header!(create_request.headers, "x-codex-window-id") == "file-bridge-window"
    assert header!(create_request.headers, "session-id") == "file-bridge-session"
    assert header!(create_request.headers, "thread-id") == "file-bridge-thread"
    assert header!(create_request.headers, "authorization") == "Bearer file-bounded-headers-token"
  end

  test "file bridge routing excludes refreshing identities", %{conn: conn} do
    setup = active_api_key_fixture()

    upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_refreshing_identity_excluded",
          file_name: "refreshing-identity.txt",
          mime_type: "text/plain"
        )
      )

    %{identity: identity} =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_refreshing_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(upstream)},
        access_token: "file-refreshing-token"
      })

    Repo.update!(Ecto.Changeset.change(identity, status: "refreshing"))

    conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "refreshing-identity.txt",
        "file_size" => 21
      })

    assert json_response(conn, 503)["error"]["code"] == "no_eligible_backend"
    assert FakeUpstream.requests(upstream) == []
  end

  @tag :upstream_file_assignment_continuity
  test "multipart create uploads and finalizes through the selected file bridge assignment", %{
    conn: conn
  } do
    setup = active_api_key_fixture()
    file_id = "file_assignment_continuity_#{System.unique_integer([:positive])}"
    filename = "assignment-continuity-private.txt"
    file_contents = "synthetic assignment continuity bytes"
    file_size = byte_size(file_contents)
    upload_url = stub_upload_put(file_id)

    fallback_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_assignment_continuity_fallback",
          file_name: "fallback.txt",
          mime_type: "text/plain"
        )
      )

    fallback_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_continuity_fallback_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(fallback_upstream)},
        access_token: "file-continuity-fallback-token"
      })

    selected_upstream = start_upstream(FakeUpstream.file_protocol_success(file_id: file_id))

    FakeUpstream.set_mode(
      selected_upstream,
      FakeUpstream.file_protocol_success(
        file_id: file_id,
        file_name: filename,
        mime_type: "text/plain",
        upload_url: upload_url
      )
    )

    selected_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_continuity_selected_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(selected_upstream)},
        access_token: "file-continuity-selected-token"
      })

    reset_at = DateTime.add(DateTime.utc_now(), 900, :second) |> DateTime.truncate(:second)

    assert {:ok, [_window]} =
             QuotaWindows.upsert_quota_windows(fallback_assignment.identity, [
               %{
                 window_kind: "primary",
                 window_minutes: 300,
                 used_percent: Decimal.new("100"),
                 reset_at: reset_at,
                 source: "codex_response_headers",
                 source_precision: "observed",
                 freshness_state: "fresh"
               }
             ])

    assert {:ok, [_window]} =
             QuotaWindows.upsert_quota_windows(selected_assignment.identity, [
               %{
                 window_kind: "primary",
                 window_minutes: 300,
                 used_percent: Decimal.new("1"),
                 reset_at: reset_at,
                 source: "codex_response_headers",
                 source_precision: "observed",
                 freshness_state: "fresh"
               }
             ])

    upload = upload_fixture(filename, "text/plain", file_contents)

    create_conn =
      conn
      |> auth(setup)
      |> post("/v1/files", %{"purpose" => "user_data", "file" => upload})

    assert %{
             "id" => ^file_id,
             "object" => "file",
             "bytes" => ^file_size,
             "filename" => ^filename,
             "purpose" => "user_data",
             "status" => "uploaded"
           } = create_body = json_response(create_conn, 200)

    refute Map.has_key?(create_body, "upload_url")

    file = Repo.get_by!(FileRecord, file_id: file_id)
    assert file.pool_upstream_assignment_id == selected_assignment.assignment.id
    assert file.upstream_identity_id == selected_assignment.identity.id
    assert file.status == "uploaded"
    assert file.finalize_status == "succeeded"

    assert [create_request, finalize_request] = FakeUpstream.requests(selected_upstream)

    assert create_request.path == "/backend-api/files"
    assert finalize_request.path == "/backend-api/files/#{file_id}/uploaded"
    assert_upload_put(file_id, "/upload/#{file_id}", file_contents, "text/plain")
    assert FakeUpstream.requests(fallback_upstream) == []

    public_create_request =
      Repo.one!(
        from request in Request,
          where:
            request.pool_id == ^setup.pool.id and request.api_key_id == ^setup.api_key.id and
              request.endpoint == "/v1/files" and request.status == "succeeded"
      )

    assert public_create_request.endpoint == "/v1/files"
    assert public_create_request.transport == "http_multipart"
    assert public_create_request.request_metadata["operation"] == "uploaded"

    routing_metadata = public_create_request.request_metadata["routing"]
    assert routing_metadata["route_class"] == "file_upload"

    assert routing_metadata["selected_bridge_candidate_id"] ==
             selected_assignment.assignment.id

    metadata_text = inspect(public_create_request.request_metadata)
    refute metadata_text =~ filename
    refute metadata_text =~ file_contents
    refute metadata_text =~ "upload_url"
    refute metadata_text =~ FakeUpstream.url(selected_upstream)
    refute metadata_text =~ setup.raw_key
    refute metadata_text =~ "file-continuity-selected-token"
    refute metadata_text =~ "file-continuity-fallback-token"
    refute metadata_text =~ "Bearer"
  end

  @tag :upstream_file_create_bridge
  test "prefers a quota-usable file bridge assignment when an earlier assignment is exhausted", %{
    conn: conn
  } do
    setup = active_api_key_fixture()

    exhausted_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_exhausted_assignment",
          file_name: "exhausted.txt",
          mime_type: "text/plain"
        )
      )

    exhausted_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_bridge_exhausted_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(exhausted_upstream)},
        access_token: "file-bridge-exhausted-token"
      })

    usable_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_usable_assignment",
          file_name: "usable.txt",
          mime_type: "text/plain"
        )
      )

    usable_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_bridge_usable_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(usable_upstream)},
        access_token: "file-bridge-usable-token"
      })

    reset_at = DateTime.add(DateTime.utc_now(), 900, :second) |> DateTime.truncate(:second)

    assert {:ok, [_window]} =
             QuotaWindows.upsert_quota_windows(
               exhausted_assignment.identity,
               [
                 %{
                   window_kind: "primary",
                   window_minutes: 300,
                   used_percent: Decimal.new("100"),
                   reset_at: reset_at,
                   source: "codex_response_headers",
                   source_precision: "observed",
                   freshness_state: "fresh"
                 }
               ]
             )

    assert {:ok, [_window]} =
             QuotaWindows.upsert_quota_windows(
               usable_assignment.identity,
               [
                 %{
                   window_kind: "primary",
                   window_minutes: 300,
                   used_percent: Decimal.new("1"),
                   reset_at: reset_at,
                   source: "codex_response_headers",
                   source_precision: "observed",
                   freshness_state: "fresh"
                 }
               ]
             )

    conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{"file_name" => "usable.txt", "file_size" => 12})

    assert %{"file_id" => "file_usable_assignment"} = json_response(conn, 200)
    assert FakeUpstream.requests(exhausted_upstream) == []
    assert [%{path: "/backend-api/files"}] = FakeUpstream.requests(usable_upstream)
  end

  @tag :upstream_file_create_bridge
  test "file bridge quota filtering batches quota-window reads for all candidates", %{conn: conn} do
    setup = active_api_key_fixture()
    reset_at = DateTime.add(DateTime.utc_now(), 900, :second) |> DateTime.truncate(:second)

    upstreams =
      for index <- 1..3 do
        upstream =
          start_upstream(
            FakeUpstream.file_protocol_success(
              file_id: "file_batched_quota_#{index}",
              file_name: "batched-quota-#{index}.txt",
              mime_type: "text/plain"
            )
          )

        assignment =
          active_upstream_assignment_fixture(setup.pool, %{
            chatgpt_account_id: "acct_file_batched_quota_#{index}_#{System.unique_integer([:positive])}",
            metadata: %{"base_url" => FakeUpstream.url(upstream)},
            access_token: "file-batched-quota-token-#{index}"
          })

        used_percent = if index == 3, do: Decimal.new("1"), else: Decimal.new("100")

        assert {:ok, [_window]} =
                 QuotaWindows.upsert_quota_windows(assignment.identity, [
                   %{
                     window_kind: "primary",
                     window_minutes: 300,
                     used_percent: used_percent,
                     reset_at: reset_at,
                     source: "codex_response_headers",
                     source_precision: "observed",
                     freshness_state: "fresh"
                   }
                 ])

        {index, upstream}
      end

    {conn, query_counts} =
      count_repo_commands(fn ->
        conn
        |> auth(setup)
        |> put_req_header("content-type", "application/json")
        |> post(~p"/backend-api/files", %{
          "file_name" => "batched-quota.txt",
          "file_size" => 12
        })
      end)

    assert %{"file_id" => "file_batched_quota_3"} = json_response(conn, 200)
    assert command_count(query_counts, "upstream_identities", "SELECT") == 2
    assert command_count(query_counts, "account_quota_windows", "SELECT") == 0

    assert [{1, first_upstream}, {2, second_upstream}, {3, third_upstream}] = upstreams
    assert FakeUpstream.requests(first_upstream) == []
    assert FakeUpstream.requests(second_upstream) == []
    assert [%{path: "/backend-api/files"}] = FakeUpstream.requests(third_upstream)
  end

  @tag :upstream_file_create_bridge
  test "rejects file bridge create when all assignments are quota exhausted", %{conn: conn} do
    setup = active_api_key_fixture()

    first_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_exhausted_first",
          file_name: "exhausted-first.txt",
          mime_type: "text/plain"
        )
      )

    first_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_exhausted_first_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(first_upstream)},
        access_token: "file-exhausted-first-token"
      })

    second_upstream =
      start_upstream(
        FakeUpstream.file_protocol_success(
          file_id: "file_exhausted_second",
          file_name: "exhausted-second.txt",
          mime_type: "text/plain"
        )
      )

    second_assignment =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_exhausted_second_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(second_upstream)},
        access_token: "file-exhausted-second-token"
      })

    reset_at = DateTime.add(DateTime.utc_now(), 900, :second) |> DateTime.truncate(:second)

    for %{identity: identity} <- [first_assignment, second_assignment] do
      assert {:ok, [_window]} =
               QuotaWindows.upsert_quota_windows(identity, [
                 %{
                   window_kind: "primary",
                   window_minutes: 300,
                   used_percent: Decimal.new("100"),
                   reset_at: reset_at,
                   source: "codex_response_headers",
                   source_precision: "observed",
                   freshness_state: "fresh"
                 }
               ])
    end

    conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{"file_name" => "quota.txt", "file_size" => 12})

    # Every assignment exhausted with a known reset: the provider's terminal
    # usage-limit answer with the earliest reset (findings#206 row 206-508).
    assert %{"error" => %{"code" => "quota_exhausted", "type" => "usage_limit_reached", "resets_in_seconds" => seconds}} =
             json_response(conn, 429)

    assert get_resp_header(conn, "retry-after") == [Integer.to_string(seconds)]
    assert FakeUpstream.requests(first_upstream) == []
    assert FakeUpstream.requests(second_upstream) == []

    request = Repo.one!(from request in Request, where: request.pool_id == ^setup.pool.id)

    assert request.status == "failed"
    assert request.request_metadata["error_code"] == "quota_exhausted"

    assert [
             %{"reasons" => [%{"reason_codes" => first_reason_codes}]},
             %{"reasons" => [%{"reason_codes" => second_reason_codes}]}
           ] = request.request_metadata["candidate_exclusions"]

    assert "exhausted" in first_reason_codes
    assert "exhausted" in second_reason_codes
  end

  @tag :upstream_file_create_error
  test "maps upstream create errors safely without persisting URLs or raw upstream bodies", %{
    conn: conn
  } do
    setup = active_api_key_fixture()

    upstream =
      start_upstream(
        FakeUpstream.file_protocol_unauthorized(
          file_id: "file_auth_error",
          unauthorized_payload: %{
            "error" => %{
              "code" => "invalid_api_key",
              "message" => "secret account detail redacted-marker"
            }
          }
        )
      )

    active_upstream_assignment_fixture(setup.pool, %{
      chatgpt_account_id: "email_sentinel@example.com",
      metadata: %{"base_url" => FakeUpstream.url(upstream)},
      access_token: "file-error-token"
    })

    conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "error-fixture.txt",
        "file_size" => 5,
        "use_case" => "codex"
      })

    body = json_response(conn, 401)
    assert body["error"]["code"] == "upstream_file_bridge_failed"
    assert body["error"]["message"] == "upstream file create failed"
    refute inspect(body) =~ "invalid_api_key"
    refute inspect(body) =~ "redacted-marker"
    refute Repo.get_by(FileRecord, file_id: "file_auth_error")

    assert [create_request] = FakeUpstream.requests(upstream)
    assert create_request.path == "/backend-api/files"
    assert header!(create_request.headers, "authorization") == "Bearer file-error-token"

    refute Enum.any?(create_request.headers, fn {name, _value} -> name == "chatgpt-account-id" end)

    requests = Repo.all(from request in Request, where: request.pool_id == ^setup.pool.id)
    refute inspect(requests) =~ "invalid_api_key"
    refute inspect(requests) =~ "redacted-marker"
    refute inspect(requests) =~ "file-error-token"
  end

  @tag :upstream_file_create_failure_routing_lifecycle
  test "records file bridge routing failure lifecycle for retryable create failures", %{
    conn: conn
  } do
    setup = active_api_key_fixture()
    upstream = start_upstream(FakeUpstream.http_500_json_error())

    %{assignment: assignment} =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_bridge_create_failure_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(upstream)},
        access_token: "file-create-failure-token"
      })

    conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "retryable-create-failure.txt",
        "file_size" => 12,
        "use_case" => "codex"
      })

    body = json_response(conn, 500)
    assert body["error"]["code"] == "upstream_file_bridge_failed"

    assert %BridgeDemotion{reason_code: "file_bridge_upstream_file_bridge_failed"} =
             Repo.get_by(BridgeDemotion,
               pool_id: setup.pool.id,
               api_key_id: setup.api_key.id,
               model_identifier: "backend-api/files",
               pool_upstream_assignment_id: assignment.id,
               status: "active"
             )

    assert %RoutingCircuitState{
             reason_code: "file_bridge_upstream_file_bridge_failed",
             route_class: "file_upload",
             failure_count: 1
           } =
             file_upload_circuit_state(setup, assignment)
  end

  @tag :upstream_file_retry_timeout_affinity
  test "retry-timeout upstream files stay ineligible for response affinity", %{conn: conn} do
    setup = active_api_key_fixture()

    upstream =
      start_upstream(
        FakeUpstream.file_protocol_finalize_retry(
          file_id: "file_retry_timeout_affinity",
          file_name: "retry-timeout.txt",
          mime_type: "text/plain"
        )
      )

    %{assignment: assignment} =
      active_upstream_assignment_fixture(setup.pool, %{
        chatgpt_account_id: "acct_file_retry_timeout_#{System.unique_integer([:positive])}",
        metadata: %{"base_url" => FakeUpstream.url(upstream)},
        access_token: "file-retry-timeout-token"
      })

    create_conn =
      conn
      |> auth(setup)
      |> put_req_header("content-type", "application/json")
      |> post(~p"/backend-api/files", %{
        "file_name" => "retry-timeout.txt",
        "file_size" => 12,
        "use_case" => "codex"
      })

    assert %{"file_id" => file_id} = json_response(create_conn, 200)

    assert {:error, %{code: :file_not_found}} =
             Files.assignment_affinities(setup, [file_id])

    old_bridge_config = Application.get_env(:codex_pooler, FileBridge, [])

    Application.put_env(
      :codex_pooler,
      FileBridge,
      Keyword.merge(old_bridge_config,
        finalize_retry_timeout_ms: 0,
        finalize_retry_interval_ms: 0
      )
    )

    finalize_conn =
      build_conn()
      |> auth(setup)
      |> post(~p"/backend-api/files/#{file_id}/uploaded", %{})

    assert %{"status" => "retry"} = json_response(finalize_conn, 200)

    file = Repo.get_by!(FileRecord, file_id: file_id)
    assert file.status == "pending_upload"
    assert file.finalize_status == "pending"
    assert is_binary(file.pool_upstream_assignment_id)

    assert {:error, %{code: :file_not_found}} =
             Files.assignment_affinities(setup, [file_id])

    assert %BridgeDemotion{reason_code: "file_bridge_retry_timeout"} =
             Repo.get_by(BridgeDemotion,
               pool_id: setup.pool.id,
               api_key_id: setup.api_key.id,
               model_identifier: "backend-api/files",
               pool_upstream_assignment_id: assignment.id,
               status: "active"
             )

    assert %RoutingCircuitState{
             reason_code: "file_bridge_retry_timeout",
             route_class: "file_upload",
             failure_count: 1
           } =
             file_upload_circuit_state(setup, assignment)

    assert [create_request, finalize_request] = FakeUpstream.requests(upstream)
    assert create_request.path == "/backend-api/files"
    assert finalize_request.path == "/backend-api/files/file_retry_timeout_affinity/uploaded"
  end

  test "returns not found for missing files", %{conn: conn} do
    setup = active_api_key_fixture()
    missing_file_id = "file-missing-sensitive-token"

    conn =
      conn
      |> auth(setup)
      |> post(~p"/backend-api/files/#{missing_file_id}/uploaded", %{})

    assert json_response(conn, 404)["error"]["code"] == "file_not_found"

    failed_request =
      Repo.one!(
        from request in Request,
          where:
            request.pool_id == ^setup.pool.id and
              request.endpoint == "/backend-api/files/uploaded"
      )

    assert failed_request.status == "failed"
    assert failed_request.request_metadata["error_code"] == "file_not_found"
    refute inspect(failed_request.request_metadata) =~ missing_file_id
  end

  defp upload_fixture(filename, content_type, contents) do
    path =
      Path.join(
        System.tmp_dir!(),
        "codex-pooler-routing-file-#{System.unique_integer([:positive])}"
      )

    File.write!(path, contents)
    on_exit(fn -> File.rm(path) end)
    %Plug.Upload{path: path, filename: filename, content_type: content_type}
  end

  defp stub_upload_put(file_id) do
    stub_name = {__MODULE__, :upload_put, file_id}
    test_pid = self()

    Req.Test.stub(stub_name, fn conn ->
      send(test_pid, {
        :upload_put,
        file_id,
        conn.method,
        conn.request_path,
        Req.Test.raw_body(conn),
        conn.req_headers
      })

      conn
      |> Plug.Conn.put_status(201)
      |> Req.Test.text("")
    end)

    current_bridge_config = Application.get_env(:codex_pooler, FileBridge, [])

    Application.put_env(
      :codex_pooler,
      FileBridge,
      Keyword.merge(current_bridge_config, upload_req_options: [plug: {Req.Test, stub_name}])
    )

    "https://fake-upload.invalid/upload/#{file_id}?sig=fake-upload"
  end

  defp assert_upload_put(file_id, path, body, content_type) do
    assert_receive {:upload_put, ^file_id, "PUT", ^path, ^body, headers}, @detection_timeout_ms
    assert header!(headers, "content-type") == content_type
    assert header!(headers, "x-ms-blob-type") == "BlockBlob"

    refute Enum.any?(headers, fn {name, _value} ->
             name in ["authorization", "cookie", "user-agent"]
           end)
  end

  defp header!(headers, name) do
    headers
    |> Enum.find_value(fn
      {^name, value} -> value
      _other -> nil
    end)
    |> case do
      nil -> flunk("missing header #{name}")
      value -> value
    end
  end

  defp file_upload_circuit_state(setup, assignment) do
    Repo.one(
      from state in RoutingCircuitState,
        where:
          state.pool_id == ^setup.pool.id and
            is_nil(state.api_key_id) and
            state.model_identifier == "backend-api/files" and
            state.pool_upstream_assignment_id == ^assignment.id and
            state.route_class == "file_upload"
    )
  end

  defp count_repo_commands(fun) do
    parent = self()
    handler_id = "backend-file-routing-test-#{System.unique_integer([:positive])}"

    # Also on_exit: a linked crash or the ExUnit timeout kills the test before `after` runs.
    on_exit(fn -> :telemetry.detach(handler_id) end)

    :ok =
      :telemetry.attach(
        handler_id,
        [:codex_pooler, :repo, :query],
        fn _event, _measurements, metadata, _config ->
          if metadata[:repo] == Repo do
            send(parent, {handler_id, metadata[:source], command_name(metadata[:query])})
          end
        end,
        nil
      )

    try do
      result = fun.()
      {result, drain_repo_commands(handler_id, %{})}
    after
      :telemetry.detach(handler_id)
    end
  end

  defp drain_repo_commands(handler_id, commands) do
    receive do
      {^handler_id, source, command} ->
        key = {source, command}
        drain_repo_commands(handler_id, Map.update(commands, key, 1, &(&1 + 1)))
    after
      0 -> commands
    end
  end

  defp command_count(commands, source, command), do: Map.get(commands, {source, command}, 0)

  defp command_name(query) when is_binary(query) do
    query
    |> String.trim_leading()
    |> String.split(~r/\s+/, parts: 2)
    |> List.first()
    |> String.upcase()
  end

  defp command_name(_query), do: nil
end
